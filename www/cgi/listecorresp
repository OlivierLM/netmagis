#!%TCLSH%

#
# List users
#
# Called by: accueil
#
# History
#   2002/12/05 : jean     : design
#   2003/05/13 : pda/jean : use auth base
#   2005/09/30 : jean     : let non-admin access this page
#   2010/12/12 : pda      : i18n
#

#
# Template pages used by this script
#

set conf(page)          listecorresp.html

#
# Script parameters
#

set conf(tableau) {
    global {
	chars {10 normal}
	columns {10 20 20 10 20 5 15}
	botbar {yes}
	align {left}
	latex {
	    linewidth {267}
	}
    }
    pattern Title {
	title {yes}
	topbar {yes}
	chars {bold}
	vbar {yes}
	column { }
	vbar {yes}
	column { }
	vbar {yes}
	column { }
	vbar {yes}
	column { }
	vbar {yes}
	column { }
	vbar {yes}
	column { }
	vbar {yes}
	column { }
	vbar {yes}
    }
    pattern Normal {
	vbar {yes}
	column { }
	vbar {yes}
	column {
	    format {raw}
	}
	vbar {yes}
	column { }
	vbar {yes}
	column { }
	vbar {yes}
	column { }
	vbar {yes}
	column { }
	vbar {yes}
	column { }
	vbar {yes}
    }
}

#
# Netmagis general library
#

source %LIBDNS%

# ::webapp::cgidebug ; exit

##############################################################################
# Main procedure
##############################################################################

proc main {} {
    global conf
    global ah

    #
    # Initialization
    #

    d init-cgi "dns" "" {} ftab dbfd tabuid

    set titrepage [mc "List of users"]

    set lines {}
    lappend lines [list "Title" \
			  [mc "Login"] \
			  [mc "Name"] \
			  [mc "Mail"] \
			  [mc "Phone"] \
			  [mc "Address"] \
			  [mc "Present"] \
			  [mc "Group"] \
    		    ]

    set u [::webapp::authuser create %AUTO%]

    set sql "SELECT login, present, groupe.nom \
		FROM global.corresp, global.groupe \
		WHERE groupe.idgrp = corresp.idgrp \
		ORDER BY login"

    pg_select $dbfd $sql tab {
            set login		$tab(login)
            set present		$tab(present)
            set nomgroupe	$tab(nom)

	    if {[catch {set n [$ah getuser $login $u]} msg]} then {
		d error [mc "Authentication base problem: %s" $msg]
	    }
	    
	    if {$n == 1} then {
		set nomprenom	 "[$u get nom] [$u get prenom]"
		set mel		 [$u get mel]
		set tel		 [$u get tel]
		set localisation [$u get adr]
	    } else {
		set nomprenom	[mc "ERROR"]
		set mel		[mc "ERROR"]
		set tel		[mc "ERROR"]
		set localisation [mc "ERROR"]
	    }

            lappend lines [list Normal \
				  $login $nomprenom $mel $tel $localisation \
				  $present $nomgroupe ]
    }

    $u destroy

    set tab [::arrgen::output "html" $conf(tableau) $lines]

    #
    # End of script: output page and close database
    #

    d result $conf(page) [list \
				[list %TITREPAGE%       $titrepage ] \
				[list %LISTECOR%        $tab ] \
			    ]
}

::webapp::cgi-exec main $debug
