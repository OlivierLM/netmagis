#!%TCLSH%

#
# Store host modifications
#
# Called by: index
#
# Parameters (form or url):
#   - display host selection page
#	- action : (none)
#   - display host edit page
#	- action : "edit"
#   - modification of host informations
#	- action : "store"
#	- confirm : "no" or "yes" (if confirm ok)
#	- idrr : original idrr of host
#	- nom : modified name of the host
#	- domaine : modified domain of the host
#	- mac : modified mac address
#	- iddhcpprofil : modified DHCP profile (or 0)
#	- hinfo : host type (id)
#	- droitsmtp : non existant, or non empty
#	- ttl : value (or empty if now allowed)
#	- commentaire : comments
#	- respnom : name of person responsible for this host
#	- respmel : mail of person responsible for this host
#
# History
#   2002/05/03 : pda/jean : design
#   2002/05/23 : pda/jean : add responsible
#   2002/07/09 : pda      : add nologin
#   2002/07/09 : pda      : convert names to lowercase
#   2003/05/13 : pda/jean : use auth base
#   2004/08/05 : pda/jean : add mac
#   2005/04/08 : pda/jean : add dhcppprofil
#   2007/10/25 : jean     : log modify actions
#   2008/02/13 : pda/jean : responsible person is the current user by default
#   2008/07/25 : pda/jean : add smtp emit right
#   2010/10/14 : pda      : add journey (via next)
#   2010/10/26 : pda      : check dhcpprofil case when no maC address
#   2010/10/26 : pda      : make name and domain editable
#   2010/10/31 : pda      : add ttl
#   2010/12/14 : pda      : i18n
#   2010/12/25 : pda      : use cgi-dispatch
#

#
# Template pages used by this script
#

set conf(page-sel)	mod-sel.html
set conf(page-edit)	mod-edit.html
set conf(page-ok)	mod-ok.html
set conf(page-smtp)	mod-smtp.html

#
# Next actions
# 

set conf(next)		"mod"
set conf(nextmap)	"net"
set conf(nextlist)	"net"
set conf(nextadd)	"add"
set conf(nextdel)	"del"

#
# Netmagis general library
#

source %LIBDNS%

# ::webapp::cgidebug ; exit

##############################################################################
# Display host selection page
##############################################################################

d cgi-register {action {}} {
} {
    global conf

    #
    # Get domains authorized for the user
    #

    set domain [menu-domain $dbfd $tabuid(idcor) "domaine" "" ""]

    #
    # End of script: output page and close database
    #

    d urlset "%URLFORM%" $conf(next) {}
    d result $conf(page-sel) [list \
				[list %DOMAINE% $domain] \
			    ]
}

##############################################################################
# Display host edit page
##############################################################################

d cgi-register {action edit} {
    {nom	1 1}
    {domaine	1 1}
} {
    global conf

    #
    # Validate form input
    #

    set name   $nom
    set domain $domaine

    set msg [check-name-syntax $name]
    if {$msg ne ""} then {
	d error $msg
    }
    set name [string tolower $name]

    set msg [check-authorized-host $dbfd $tabuid(idcor) $name $domain trr "existing-host"]
    if {$msg ne ""} then {
	d error $msg
    }

    #
    # Check host existence
    #

    if {$trr(idrr) eq ""} then {
	d error [mc "Name '%s' does not exist" $name]
    }

    #
    # In order to display original name of host
    #

    set vname $name
    set vdomain $domain

    #
    # Default values for all fields
    #

    set name [::webapp::form-text nom 1 20 64 $name]
    set domain [menu-domain $dbfd $tabuid(idcor) domaine "" $domain]

    set val [::webapp::html-string $trr(mac)]
    set mac [::webapp::form-text mac 1 20 17 $val]
    lassign [menu-dhcp-profile $dbfd iddhcpprofil $tabuid(idcor) $trr(iddhcpprofil)] \
	dhcpprofillibelle dhcpprofilmenu

    set menuhinfo [menu-hinfo $dbfd hinfo $trr(hinfo)]

    lassign [menu-droitsmtp $dbfd "droitsmtp" tabuid $trr(droitsmtp)] \
	droitsmtplibelle droitsmtpmenu

    lassign [menu-ttl $dbfd "ttl" tabuid $trr(ttl)] ttllibelle ttlval

    set val [::webapp::html-string $trr(commentaire)]
    set commentaire [::webapp::form-text commentaire 1 50 50 $val]

    set val [::webapp::html-string $trr(respnom)]
    set respnom [::webapp::form-text respnom 1 50 50 $val]

    set val [::webapp::html-string $trr(respmel)]
    set respmel [::webapp::form-text respmel 1 50 50 $val]

    #
    # Next script
    #

    d urlset "%URLFORM%" $conf(next) {}
    d urladdnext "%URLFORM%"

    d urlset "%URLADD%" $conf(nextadd) {}
    d urlset "%URLDEL%" $conf(nextdel) {}

    #
    # End of script: output page and close database
    #

    d result $conf(page-edit) [list \
					[list %IDRR% $trr(idrr)] \
					[list %VNOM% $vname] \
					[list %VDOMAINE% $vdomain] \
					[list %NOM% $name] \
					[list %DOMAINE% $domain] \
					[list %MAC% $mac] \
					[list %DHCPPROFILLIBELLE% $dhcpprofillibelle] \
					[list %DHCPPROFILMENU% $dhcpprofilmenu] \
					[list %MENUHINFO% $menuhinfo] \
					[list %DROITSMTPLIBELLE% $droitsmtplibelle] \
					[list %DROITSMTPMENU% $droitsmtpmenu] \
					[list %TTLLIBELLE% $ttllibelle] \
					[list %TTLVAL% $ttlval] \
					[list %COMMENTAIRE% $commentaire] \
					[list %RESPNOM% $respnom] \
					[list %RESPMEL% $respmel] \
			    ]
}

##############################################################################
# Store modifications
##############################################################################

# History
#   2002/05/03 : pda/jean : design
#

d cgi-register {action store} {
    {confirm	1 1}
    {idrr	1 1}
    {nom	1 1}
    {domaine	1 1}
    {mac	1 1}
    {iddhcpprofil 1 1}
    {hinfo	1 1}
    {droitsmtp	0 1}
    {ttl	1 1}
    {commentaire 1 1}
    {respnom	1 1}
    {respmel	1 1}
} {
    global conf

    set login $tabuid(login)
    set idcor $tabuid(idcor)

    #
    # Get dns update interval
    #

    set interval [dnsconfig get "dnsupdateperiod"]

    #
    # Check host idrr
    # It must not be an alias, and user must own all addresses
    #

    if {! [regexp {^\d+$} $idrr]} then {
	d error [mc "Invalid input '%s'" $idrr]
    }
    if {! [read-rr-by-id $dbfd $idrr trr]} then {
	d error [mc "Cannot read host-id %s" $idrr]
    }
    # test if host exists
    if {$trr(idrr) eq ""} then {
	d error [mc "Host id %s not found" $idrr]
    }

    set msg [check-authorized-host $dbfd $idcor $trr(nom) $trr(domaine) trr "existing-host"]
    if {$msg ne ""} then {
	d error $msg
    }

    #
    # Check new name and domain
    #

    set msg [check-name-syntax $nom]
    if {$msg ne ""} then {
	d error $msg
    }
    set nom [string tolower $nom]

    set msg [check-authorized-host $dbfd $idcor $nom $domaine ntrr "host"]
    if {$msg ne ""} then {
	d error $msg
    }
    if {$ntrr(idrr) != $idrr} then {
	#
	# New host or domain. We must check it does not already exist.
	#
    	if {[info exists ntrr(ip)] && [llength $ntrr(ip)] > 0} then {
	    d error [mc "Host '%s' already exists" "$nom.$domaine"]
	}
    }
    set iddom $ntrr(iddom)

    #
    # Check various informations
    #

    if {$mac ne ""} then {
	set msg [check-mac-syntax $dbfd $mac]
	if {$msg ne ""} then {
	    d error $msg
	}
    }
    set msg [check-static-dhcp $dbfd $mac $trr(ip)]
    if {$msg ne ""} then {
	d error $msg
    }

    if {! [check-iddhcpprofil $dbfd $iddhcpprofil dhcpprofil msg]} then {
	d error $msg
    }
    if {$mac eq "" && $iddhcpprofil != 0} then {
	d error [mc "You cannot set a DHCP profile without assigning a MAC address"]
    }

    set idhinfo [read-hinfo $dbfd $hinfo]
    if {$idhinfo == -1} then {
	d error [mc "Host type '%s' not found" $hinfo]
    }

    if {$tabuid(droitsmtp)} then {
	if {$droitsmtp eq ""} then {
	    set droitsmtp 0
	} else {
	    set droitsmtp 1
	}
    } else {
	set droitsmtp 0
    }

    if {$tabuid(droitttl)} then {
	if {$ttl eq ""} then {
	    set ttl -1
	} else {
	    set msg [check-ttl $ttl]
	    if {$msg ne ""} then {
		d error $msg
	    }
	}
    } else {
	set ttl -1
    }

    #
    # If responsible person is not specified, this is the user by default
    #

    if {$respnom eq "" && $respmel eq ""} then {
	set respnom "$tabuid(nom) $tabuid(prenom)"
	set respmel $tabuid(mel)
    }

    #
    # Ask for confirmation if user asked for SMTP emit right
    # (and this right was now allowed before)
    #

    if {! $trr(droitsmtp) && $droitsmtp && $confirm ne "yes"} then {
	#
	# Confirmation page
	#

	set l {idrr nom domaine mac iddhcpprofil hinfo droitsmtp ttl
			commentaire respnom respmel}
	set hidden [::webapp::hide-parameters $l ftab]
	d urlset "%URLFORM%" $conf(next) {}
	d urladdnext "%URLFORM%"
	d result $conf(page-smtp) [list \
						[list %HIDDEN% $hidden] \
			    ]
	return 0
    }

    #
    # Store modificationas
    #

    d dblock {dns.rr}

    if {! $tabuid(droitsmtp)} then {
	set droitsmtp $trr(droitsmtp)
    }
    if {! $tabuid(droitttl)} then {
	set ttl $trr(ttl)
    }

    if {$mac eq ""} then {
	set qmac NULL
    } else {
	set qmac "'[::pgsql::quote $mac]'"
    }
    if {$iddhcpprofil == 0} then {
	set qiddhcpprofil NULL
    } else {
	set qiddhcpprofil $iddhcpprofil
    }
    if {$ttl == -1} then {
	set httl [mc "(default zone value)"]
    } else {
	set httl $ttl
    }

    set qnom         [::pgsql::quote $nom]
    set qcommentaire [::pgsql::quote $commentaire]
    set qrespnom     [::pgsql::quote $respnom]
    set qrespmel     [::pgsql::quote $respmel]
    set sql "UPDATE dns.rr SET
			    nom = '$qnom',
			    iddom = $iddom,
			    mac = $qmac,
			    iddhcpprofil = $qiddhcpprofil,
			    idhinfo = $idhinfo,
			    droitsmtp = $droitsmtp,
			    ttl = $ttl,
			    commentaire = '$qcommentaire',
			    respnom = '$qrespnom',
			    respmel = '$qrespmel'
			WHERE idrr = $trr(idrr)"
    if {! [::pgsql::execsql $dbfd $sql msg]} then {
       d dbabort [mc "modify %s" "$nom.$domaine"] $msg
    }

    set msg [touch-rr $dbfd $trr(idrr)]
    if {$msg ne ""} then {
	d dbabort [mc "modify %s" [mc "RR"]] $msg
    }

    d dbcommit [mc "modify %s" "$nom.$domaine"]

    #
    # Update log
    #

    set m "modify $trr(nom).$trr(domaine):"
    foreach c {nom domaine mac dhcpprofil hinfo droitsmtp ttl commentaire respnom respmel} {
	append m " $c=[set $c]"
    }
    d writelog "modrr" $m

    #
    # Display result
    #

    set commentaire [html-tab-string $commentaire]
    set respnom     [html-tab-string $respnom]
    set respmel     [html-tab-string $respmel]

    if {$droitsmtp} then {
	set droitsmtp [mc "Yes"]
    } else {
	set droitsmtp [mc "No"]
    }

    #
    # Prepare next step in journey
    #

    switch -- [d nextprog] {
	map {
	    d urlset "%URLSUITE%" $conf(nextmap) [list {domap {yes}} [d nextargs]]
	}
	list {
	    d urlset "%URLSUITE%" $conf(nextlist) [list {dolist {yes}} [d nextargs]]
	}
	default {
	    d urlset "%URLSUITE%" $conf(next) {}
	}
    }

    #
    # End of script: output page and close database
    #

    d result $conf(page-ok) [list \
					[list %NOM% $nom] \
					[list %DOMAINE% $domaine] \
					[list %MAC% $mac] \
					[list %DHCPPROFIL% $dhcpprofil] \
					[list %HINFO% $hinfo] \
					[list %DROITSMTP% $droitsmtp] \
					[list %TTL% $httl] \
					[list %COMMENTAIRE% $commentaire] \
					[list %RESPNOM% $respnom] \
					[list %RESPMEL% $respmel] \
					[list %INTERVAL% $interval] \
			    ]
    return 0
}

##############################################################################
# Main procedure
##############################################################################

d cgi-dispatch "dns" ""
