#!%TCLSH%

#
# Validates a pair login/password from a user
#
# Parameters (form or url): none
#
# History
#   2014/04/11 : pda/jean : design
#

#
# Template pages used by this script
#

set conf(page-fail)	login.html

#
# Next actions
# 

set conf(next-fail)	"login"
set conf(next-index)	"index"

#
# Netmagis general library
#

source %LIBNETMAGIS%

# ::webapp::cgidebug ; exit

#
# Check user password against the crypted password stored in database
#

proc check-password {dbfd login upw} {
    set success false

    set am [dnsconfig get "authmethod"]
    switch $am {
	pgsql {
	    set qlogin [::pgsql::quote $login]
	    set sql "SELECT password FROM pgauth.user WHERE login = '$qlogin'"
	    set dbpw ""
	    pg_select $dbfd $sql tab {
		set dbpw $tab(password)
	    }

	    set success [pgauth-checkpw $upw $dbpw]
	}
	ldap {
	    set url       [dnsconfig get "ldapurl"]
	    set basedn    [dnsconfig get "ldapbasedn"]
	    set attrlogin [dnsconfig get "ldapattrlogin"]

	    set userdn "$attrlogin=$login,$basedn"

	    set handle [::ldapx::ldap create %AUTO%]
	    if {[$handle connect $url $userdn $upw]} then {
		set success true
		$handle disconnect
	    }
	}
    }

    return $success
}

##############################################################################
# Display login page and validate access
##############################################################################

d cgi-register {} {
    {login	0 1}
    {logout	0 1}
    {pw		0 1}
} {
    global conf

    set message ""
    if {$logout ne ""} then {
	if {$uid eq "-"} then {
	    set message ""
	} else {
	    set qtoken [::pgsql::quote [::webapp::get-cookie "session"]]
	    set message [register-user-logout $dbfd [d uid] $qtoken "" "logout"]
	    if {$message eq ""} then {
		set message [mc "Logout successful"]
	    }
	    d writelog "auth" "logout [d uid] $qtoken"

	    d uid "-"
	    d euid {- -1}

	    # leave login unmodified for the "login" page
	}
    } else {
	if {[string trim $login] ne ""} then {
	    set ok [check-password $dbfd $login $pw]
	    if {$ok} then {
		set msg [register-user-login $dbfd $login token idcor]
		if {$msg eq ""} then {
		    ::webapp::set-cookie "session" $token 0 "" "" 0 0

		    array set ftab2 {
			lastlogin {{yes}}
		    }
		    puts stdout [::webapp::call-cgi [pwd]/$conf(next-index) ftab2]
		    exit 0
		} else {
		    set message $msg
		}
	    } else {
		set message [mc "Login failed"]
	    }
	}
    }

    #
    # Restitute form parameters
    #

    set login [::webapp::html-string $login]

    #
    # End of script: output page and close database
    #

    d urlset "%URLFORM%" $conf(next-fail) {}
    d result $conf(page-fail) [list \
				    [list %MESSAGE% $message] \
				    [list %LOGIN%   $login] \
    				]
}

##############################################################################
# Main procedure
##############################################################################

d cgi-dispatch "anon" ""
