#!%TCLSH%

#
# Validates a pair login/password from a user
#
# Parameters (form or url): none
#
# History
#   2014/04/11 : pda/jean : design
#

#
# Template pages used by this script
#

set conf(page-fail)	login.html

#
# Next actions
# 

set conf(next-fail)	"login"

#
# Netmagis general library
#

source %LIBNETMAGIS%

# ::webapp::cgidebug ; exit

#
# Check user password against the crypted password stored in database
#

proc check-password {dbfd login upw} {
    set success false
    set qlogin [::pgsql::quote $login]
    set sql "SELECT password FROM pgauth.user WHERE login = '$qlogin'"
    set dbpw ""
    pg_select $dbfd $sql tab {
	set dbpw $tab(password)
    }

    if {[regexp {^\$1\$([^\$]+)\$} $dbpw dummy salt]} then {
	set crypted [exec openssl passwd -1 -salt $salt $upw]
	if {$crypted eq $dbpw} then {
	    set success true
	}
    }

    return $success
}

##############################################################################
# Display login page and validate access
##############################################################################

d cgi-register {} {
    {login	0 1}
    {pw		0 1}
    {param	0 1}
} {
    global conf

    set message ""
    if {[string trim $login] ne ""} then {
	set ok [check-password $dbfd $login $pw]
	if {$ok} then {
	    set msg [create-authtoken $dbfd $login token]
	    if {$msg eq ""} then {
		::webapp::set-cookie "session" $token 0 "" "" 0 0

		set message [mc "Auth OK"]
	    } else {
		set message $msg
	    }
	} else {
	    set message [mc "Login failed"]
	}

    }

    #
    # Restitute form parameters
    #

    set param [::webapp::html-string $param]
    set login [::webapp::html-string $login]

    #
    # End of script: output page and close database
    #

    d urlset "%URLFORM%" $conf(next-fail) {}
    d result $conf(page-fail) [list \
				    [list %MESSAGE% $message] \
				    [list %PARAM%   $param] \
				    [list %LOGIN%   $login] \
    				]
}

##############################################################################
# Main procedure
##############################################################################

d cgi-dispatch "anon" ""
