#!%TCLSH%

#
# Modify group permissions
#
# Called by: admin
#
# Parameters (form or url):
#   - group selection
#	- action : (empty)
#   - group edit
#	- action : "edit"
#   - group modification (add, del or mod)
#	- action : "mod"
#	- orggrp : original group name, or "::nouveau"
#	- newgrp : modified group name
#	- admin : administration permission (0 or 1)
#	- droitsmtp : permission to authorize hosts to emit with SMTP (0 or 1)
#	- droitttl : permission to modify hosts TTL (0 or 1)
#	- droitmac : permission to use MAC module (0 or 1)
#	- droitgenl : permission to generate a link number (0 or 1)
#	- confirm : yes or no
#	- loginN : group members
#	- viewnameN : valid views
#	- viewsortN : view sort class (if empty, we have to remove the view)
#	- viewselN : selected view (0 or 1)
#	- domainN : valid domains
#	- sortdomN : domain sort class (if empty, we have to remove the domain)
#	- rolemailN : permission to edit "mail roles" for this domain (0 or 1)
#	- netN : network ids for this group
#	- sortnetN : network sort class (if empty, we have to remove this network)
#	- dhcpN : permission to edit DHCP ranges for this network (0 or 1)
#	- aclN : permission to edit ACL for this netwok (0 or 1)
#	- addrN et allow_denyN : IP permissions for this group
#	- sortdhcpprofN : DHCP profile sort class (if empty, we have to remove this DHCP profile)
#	- nomdhcpprofN : DHCP profile name
#	- eqrwN : type of permission (read:0 or write:1) on equipments
#	- eqallowN : allow/deny for equipments (0 or 1)
#	- eqpatN : regexp giving equipment permission (if empty, we have to remove this permission)
#
# History
#   2002/05/21 : pda/jean : design
#   2002/07/09 : pda      : add nologin
#   2003/05/13 : pda/jean : use auth base
#   2004/01/14 : pda/jean : add IPv6
#   2004/02/12 : pda/jean : add roles
#   2004/08/06 : pda/jean : extend network permissions
#   2005/04/08 : pda/jean : DHCP profiles
#   2007/10/09 : pda/jean : renaming admgrpedit
#   2007/10/10 : pda/jean : centralization of group administration
#   2008/07/23 : pda/jean : add droitsmtp
#   2010/10/31 : pda      : add droitttl
#   2010/11/03 : pda/jean : add dr_eq
#   2010/11/30 : pda/jean : add droitmac
#   2010/12/06 : pda      : i18n
#   2010/12/26 : pda      : use cgi-dispatch
#   2012/01/21 : jean     : add droitgenl
#

#
# Template pages used by this script
#

set conf(page-sel)	admgrp-sel.html
set conf(page-edit)	admgrp-edit.html
set conf(page-conf)	admgrp-conf.html
set conf(page-confdel)	admgrp-confdel.html
set conf(page-del)	admgrp-del.html
set conf(page-mod)	admgrp-mod.html

#
# Next actions
# 

set conf(next)		"admgrp"

#
# Script parameters
#

# number of lines in listboxes
set conf(height)	20

set conf(form) {
	{orggrp		1 1}
}

set conf(tabuidresp) {
    global {
	chars {12 normal}
	botbar {no}
	columns {50 50}
	align {right}
	format {raw}
    }
    pattern Normal {
	topbar {no}
	vbar {no}
	format {raw}
	column { }
	vbar {no}
	column { 
	    align {left}
	}
	vbar {no}
    }
}

set conf(tabdomains) {
    global {
	chars {12 normal}
	botbar {no}
	columns {33 33 33}
	align {center}
	format {raw}
    }
    pattern Title {
	topbar {no}
	vbar {no}
	chars {bold}
	column { }
	vbar {no}
	column { }
	vbar {no}
	column { }
	vbar {no}
    }
    pattern Normal {
	topbar {no}
	vbar {no}
	format {raw}
	column { }
	vbar {no}
	column { }
	vbar {no}
	column { }
	vbar {no}
    }
}

set conf(tabviews) $conf(tabdomains)

set conf(tabnetworks) {
    global {
	chars {12 normal}
	botbar {no}
	columns {14 58 14 14}
	align {center}
    }
    pattern Title {
	topbar {no}
	vbar {no}
	chars {bold}
	column { }
	vbar {no}
	column { }
	vbar {no}
	column { }
	vbar {no}
	column { }
	vbar {no}
    }
    pattern Normal {
	topbar {no}
	vbar {no}
	format {raw}
	column { }
	vbar {no}
	column { }
	vbar {no}
	column { }
	vbar {no}
	column { }
	vbar {no}
    }
}

set conf(tabpip) {
    global {
	chars {12 normal}
	botbar {no}
	columns {20 80}
	format {raw}
    }
    pattern Normal {
	topbar {no}
	vbar {no}
	column {
	    align {right}
	}
	vbar {no}
	column {
	    align {left}
	}
	vbar {no}
    }
}

set conf(tabdreq) {
    global {
	chars {12 normal}
	botbar {no}
	columns {10 10 80}
	format {raw}
    }
    pattern Normal {
	topbar {no}
	vbar {no}
	column {
	    align {right}
	}
	vbar {no}
	column {
	    align {right}
	}
	vbar {no}
	column {
	    align {left}
	}
	vbar {no}
    }
}

set conf(tabdhcpprofil) {
    global {
	chars {12 normal}
	botbar {no}
	columns {20 80}
	format {raw}
    }
    pattern Title {
	topbar {no}
	vbar {no}
	chars {bold}
	column {
	    align {center}
	}
	vbar {no}
	column {
	    align {left}
	}
	vbar {no}
    }
    pattern Normal {
	topbar {no}
	vbar {no}
	column {
	    align {center}
	}
	vbar {no}
	column {
	    align {left}
	}
	vbar {no}
    }
}


#
# Netmagis general library
#

source %LIBNETMAGIS%

# ::webapp::cgidebug ; exit


##############################################################################
# Utility functions
##############################################################################

#
# Validate group name and get it's group id
#

proc val-group {dbfd group exist} {
    global conf

    set qgroup [::pgsql::quote $group]
    set idgrp -1
    pg_select $dbfd "SELECT idgrp FROM global.groupe WHERE nom = '$qgroup'" tab {
	set idgrp $tab(idgrp)
    }

    if {$exist} then {
	#
	# We want an existing group
	#
	if {$idgrp == -1} then {
	    d error [mc "Group '%s' not found" $group]
	}
    } else {
	#
	# We want a non-existing group
	#
	set msg [check-group-syntax $group]
	if {$msg ne ""} then {
	    d error $msg
	}
	# ... and now, check that the group is unknown
	if {$idgrp != -1} then {
	    d error [mc "Group '%s' already exist" $group]
	}
    }

    return $idgrp
}

#
# Group removal
#

proc del-group {dbfd idgrp idorphan} {
    set ltab {global.groupe global.corresp
		dns.p_network dns.p_ip dns.p_dom
		dns.dr_dhcpprofil}
    d dblock $ltab

    #
    # Remove permissions
    #

    foreach table {dns.p_network dns.p_ip dns.p_dom dns.dr_dhcpprofil} {
	set sql "DELETE FROM $table WHERE idgrp = $idgrp"
	if {! [::pgsql::execsql $dbfd $sql msg]} then {
	    d dbabort [mc "delete %s" $table] $msg
	}
    }

    #
    # Get all users which must become orphans
    #

    set sql "SELECT corresp.idcor
		FROM global.corresp, dns.rr
		WHERE corresp.idgrp = $idgrp AND rr.idcor = corresp.idcor
		GROUP BY corresp.idcor"
    
    set lidcor {}
    pg_select $dbfd $sql tab {
	lappend lidcor $tab(idcor)
    }

    # 
    # Reassign them to the group of orphans
    # 

    if {[llength $lidcor] > 0} then {
	set lcor [join $lidcor ","]
	set sql "UPDATE global.corresp SET idgrp = $idorphan, present = 0
		    WHERE idcor IN ($lcor)"
	if {! [::pgsql::execsql $dbfd $sql msg]} then {
	    d dbabort [mc "modify %s" "global.corresp"] $msg
	} 
    }

    #
    # Remove other users and the group itself
    #

    foreach table {global.corresp global.groupe} {
	set sql "DELETE FROM $table WHERE idgrp = $idgrp"
	if {! [::pgsql::execsql $dbfd $sql msg]} then {
	    d dbabort [mc "delete %s" $table] $msg
	}
    }

    d dbcommit [mc "delete %s" $idgrp]
}

##############################################################################
# Display group selection page
##############################################################################

d cgi-register {action {}} {} {
    global conf

    #
    # Get group list and convert it to a menu
    #

    set lgroup [::pgsql::getcols $dbfd "global.groupe" "nom <> ''" "nom ASC" \
						{nom nom}]
    set lgroup [linsert $lgroup 0 [list "::nouveau" [mc "Create group..."]]]
    set menuorggrp [::webapp::form-menu orggrp 1 0 $lgroup {0}]

    #
    # End of script: output page and close database
    #

    d urlset "%URLFORM%" $conf(next) {}
    d result $conf(page-sel) [list \
				[list %MENUORGGRP% $menuorggrp] \
			    ]
}

##############################################################################
# Display group edit page
##############################################################################

d cgi-register {action edit} {
    {orggrp	1 1}
} {
    global conf

    #
    # Check group name, and get group id
    #

    if {$orggrp eq "::nouveau"} then {
	set title [mc "New group creation"]
	set newgrp ""
	set msggrp [mc "Type the name of group to create"]
	set idgrp -1
	set admin 0
	set droitsmtp 0
	set droitttl 0
	set droitmac 0
	set droitgenl 0
    } else {
	set qgroup [::webapp::html-string $orggrp]
	set title [mc "Edition of group '%s'" $qgroup]
	set newgrp $qgroup
	set msggrp [mc "Modify group name or erase it to remove the group"]
	set pqgroup [::pgsql::quote $orggrp]
	set idgrp -1
	set sql "SELECT idgrp, admin, droitsmtp, droitttl, droitmac, droitgenl
			FROM global.groupe
			WHERE nom = '$pqgroup'"
	pg_select $dbfd $sql tab {
	    set idgrp $tab(idgrp)
	    set admin $tab(admin)
	    set droitsmtp $tab(droitsmtp)
	    set droitttl $tab(droitttl)
	    set droitmac $tab(droitmac)
	    set droitgenl $tab(droitgenl)
	}

	if {$idgrp == -1} then {
	    d error [mc "Group '%s' not found" $orggrp]
	}
    }

    set yes [mc "yes"]
    set no [mc "no"]
    set fmt "%1\$s $yes &nbsp; &nbsp; &nbsp;   %2\$s $no"

    set admin [::webapp::form-yesno "admin" $admin $fmt]
    set droitsmtp [::webapp::form-yesno "droitsmtp" $droitsmtp $fmt]
    set droitttl [::webapp::form-yesno "droitttl" $droitttl $fmt]
    set droitmac [::webapp::form-yesno "droitmac" $droitmac $fmt]
    set droitgenl [::webapp::form-yesno "droitgenl" $droitgenl $fmt]

    #
    # Extract the list of users belonging to this group
    #

    set lines {}

    set nlogin 1
    foreach login [::pgsql::getcols $dbfd global.corresp "idgrp = $idgrp" "login ASC" {login}] {
	set n [read-user $dbfd $login tab comment]
	if {$n == 1} then {
	    set comment "$tab(prenom) $tab(nom)"
	}
	set hlogin [::webapp::form-text login$nlogin 1 20 50 $login]
	lappend lines [list Normal $hlogin "($comment)"]
	incr nlogin
    }

    for {set i 1} {$i <= 5} {incr i} {
	set hlogin [::webapp::form-text login$nlogin 1 20 50 ""]
	lappend lines [list Normal $hlogin ""]
	incr nlogin
    }

    set listecor [::arrgen::output "html" $conf(tabuidresp) $lines]

    #
    # Extract view list, and select those which are already authorized
    # for this group.
    #

    set lines {}
    lappend lines [list "Title" \
			    [mc "Sort class"] \
			    [mc "Name"] \
			    [mc "Selected by default"] \
			]
    set lview [::pgsql::getcols $dbfd dns.view "" "name ASC" {name name}]
    set sql "SELECT view.name AS name, p_view.sort, p_view.selected
			FROM dns.view, dns.p_view
			WHERE view.idview = p_view.idview
				AND p_view.idgrp = $idgrp
			ORDER BY p_view.sort ASC, view.name ASC"
    set nview 1
    pg_select $dbfd $sql tab {
	set v        $tab(name)
	set sort     $tab(sort)
	set selected $tab(selected)

	set idx [lsearch -exact $lview [list $v $v]]
	if {$idx == -1} then {
	    d error [mc "Group has access to view '%s' which do not exists in database" $v]
	}
	set hsort [::webapp::form-text viewsort$nview 1 5 5 $sort]
	set hview [::webapp::form-menu viewname$nview 1 0 $lview [list $idx]]
	set hsel  [::webapp::form-bool viewsel$nview $selected]
	lappend lines [list Normal $hsort $hview $hsel]
	incr nview
    }

    for {set i 1} {$i <= 5} {incr i} {
	set hsort [::webapp::form-text viewsort$nview 1 5 5 ""]
	set hview [::webapp::form-menu viewname$nview 1 0 $lview {}]
	set hsel  [::webapp::form-bool viewsel$nview 0]
	lappend lines [list Normal $hsort $hview $hsel]
	incr nview
    }

    set listviews [::arrgen::output "html" $conf(tabviews) $lines]

    #
    # Extract domain list, and select those which are already authorized
    # for this group.
    #

    set lines {}
    lappend lines [list "Title" \
			    [mc "Sort class"] \
			    [mc "Domain"] \
			    [mc "Mail role edition"] \
			]
    set ldom [::pgsql::getcols $dbfd dns.domain "" "name ASC" {name name}]
    set sql "SELECT domain.name AS name, p_dom.sort, p_dom.rolemail
			FROM dns.domain, dns.p_dom
			WHERE domain.iddom = p_dom.iddom
				AND p_dom.idgrp = $idgrp
			ORDER BY p_dom.sort ASC, domain.name ASC"
    set ndom 1
    pg_select $dbfd $sql tab {
	set d        $tab(name)
	set sort     $tab(sort)
	set rolemail $tab(rolemail)

	set idx [lsearch -exact $ldom [list $d $d]]
	if {$idx == -1} then {
	    d error [mc "Group has access to domain '%s' which do not exists in database" $d]
	}
	set hsort [::webapp::form-text sortdom$ndom 1 5 5 $sort]
	set hdom  [::webapp::form-menu domain$ndom 1 0 $ldom [list $idx]]
	set hmail [::webapp::form-bool rolemail$ndom $rolemail]
	lappend lines [list Normal $hsort $hdom $hmail]
	incr ndom
    }

    for {set i 1} {$i <= 5} {incr i} {
	set hsort [::webapp::form-text sortdom$ndom 1 5 5 ""]
	set hdom  [::webapp::form-menu domain$ndom 1 0 $ldom {}]
	set hmail [::webapp::form-bool rolemail$ndom 0]
	lappend lines [list Normal $hsort $hdom $hmail]
	incr ndom
    }

    set listdomains [::arrgen::output "html" $conf(tabdomains) $lines]

    #
    # Extract network list and select those which are authorized for the group
    #

    set lines {}
    lappend lines [list "Title" \
			[mc "Sort class"] \
			[mc "Networks"] \
			[mc "DHCP management"] \
			[mc "ACL management"] \
		    ]
    set lnet {}
    set idx 0
    set sql "SELECT idnet, name, addr4, addr6 FROM dns.network
    				ORDER BY addr4, addr6"
    pg_select $dbfd $sql tab {
	set net [format "%s\t%s\t(%s)" \
			    $tab(addr4) $tab(addr6) \
			    $tab(name) \
			]
	lappend lnet [list $tab(idnet) $net]
	set idxnet($tab(idnet)) $idx
	incr idx
    }

    set sql "SELECT p.idnet, p.sort, p.dhcp, p.acl
		FROM dns.network n, dns.p_network p
		WHERE n.idnet = p.idnet AND p.idgrp = $idgrp
		ORDER BY p.sort ASC, n.addr4 ASC, n.addr6 ASC"
    set nnet 1
    pg_select $dbfd $sql tab {
	set idnet  $tab(idnet)
	set sort   $tab(sort)
	set dhcp   $tab(dhcp)
	set acl    $tab(acl)

	if {! [info exists idxnet($idnet)]} then {
	    d error [mc "Group has access to network '%s' which do not exists in database" $idnet]
	}
	set idx $idxnet($idnet)

	set hsort [::webapp::form-text sortnet$nnet 1 5 5 $sort]
	set hnet  [::webapp::form-menu net$nnet 1 0 $lnet [list $idx]]
	set hdhcp [::webapp::form-bool dhcp$nnet $dhcp]
	set hacl  [::webapp::form-bool acl$nnet $acl]

	lappend lines [list Normal $hsort $hnet $hdhcp $hacl]
	incr nnet
    }

    for {set i 1} {$i <= 5} {incr i} {
	set hsort [::webapp::form-text sortnet$nnet 1 5 5 ""]
	set hnet  [::webapp::form-menu net$nnet 1 0 $lnet {}]
	set hdhcp [::webapp::form-bool dhcp$nnet 0]
	set hacl  [::webapp::form-bool acl$nnet  0]
	lappend lines [list Normal $hsort $hnet $hdhcp $hacl]
	incr nnet
    }

    set listnets [::arrgen::output "html" $conf(tabnetworks) $lines]

    #
    # Permissions
    #

    set lines {}
    set n 1
    set sql "SELECT addr, allow_deny \
			FROM dns.p_ip \
			WHERE idgrp = $idgrp \
			ORDER BY addr"
    pg_select $dbfd $sql tab {
	set a $tab(allow_deny)
	set menuallow [::webapp::form-menu allow$n 1 0 \
					{{1 allow} {0 deny}} \
					[list [expr 1 - $a]] \
				    ]
	set textcidr [::webapp::form-text addr$n 1 49 49 $tab(addr)]
	lappend lines [list Normal $menuallow $textcidr]
	incr n
    }

    for {set i 0} {$i < 5} {incr i} {
	set menuallow [::webapp::form-menu allow$n 1 0 \
					{{1 allow} {0 deny}} \
					{0} \
				    ]
	set textcidr [::webapp::form-text addr$n 1 49 49 ""]
	lappend lines [list Normal $menuallow $textcidr]
	incr n
    }

    set listperms [::arrgen::output "html" $conf(tabpip) $lines]

    #
    # Permissions on equipments (topo)
    #

    set lines {}
    set n 1
    set sql "SELECT rw, pattern, allow_deny \
			FROM topo.dr_eq \
			WHERE idgrp = $idgrp \
			ORDER BY rw, allow_deny DESC, pattern"
    pg_select $dbfd $sql tab {
	set a $tab(rw)
	set menurw    [::webapp::form-menu eqrw$n 1 0 \
					{{0 read} {1 write}} \
					[list $a] \
				    ]
	set a $tab(allow_deny)
	set menuallow [::webapp::form-menu eqallow$n 1 0 \
					{{1 allow} {0 deny}} \
					[list [expr 1 - $a]] \
				    ]
	set pattern [::webapp::form-text eqpat$n 1 70 200 $tab(pattern)]
	lappend lines [list Normal $menurw $menuallow $pattern]
	incr n
    }

    for {set i 0} {$i < 5} {incr i} {
	set menurw [::webapp::form-menu eqrw$n 1 0 \
					{{0 read} {1 write}} \
					{0} \
				    ]
	set menuallow [::webapp::form-menu eqallow$n 1 0 \
					{{1 allow} {0 deny}} \
					{0} \
				    ]
	set pattern [::webapp::form-text eqpat$n 1 70 200 ""]
	lappend lines [list Normal $menurw $menuallow $pattern]
	incr n
    }

    set listdreq [::arrgen::output "html" $conf(tabdreq) $lines]

    #
    # DHCP profiles
    #

    set lines {}
    lappend lines [list "Title" [mc "Sort class"] [mc "DHCP profile"]]
    set lprof [::pgsql::getcols $dbfd dns.dhcpprofil "" "nom ASC" {nom nom}]
    set sql "SELECT p.nom AS nom, dr.tri AS sort
			FROM dns.dr_dhcpprofil dr, dns.dhcpprofil p
			WHERE dr.idgrp = $idgrp
			    AND dr.iddhcpprofil = p.iddhcpprofil
			ORDER BY dr.tri ASC, p.nom ASC"
    set nprof 1
    pg_select $dbfd $sql tab {
	set p        $tab(nom)
	set sort     $tab(sort)

	set idx [lsearch -exact $lprof [list $p $p]]
	if {$idx == -1} then {
	    d error [mc "Group has access to DHCP profile '%s' which do not exist in the database" $d]
	}
	set hsort [::webapp::form-text sortdhcpprof$nprof 1 5 5 $sort]
	set hprof [::webapp::form-menu nomdhcpprof$nprof 1 0 $lprof [list $idx]]
	lappend lines [list Normal $hsort $hprof]
	incr nprof
    }

    for {set i 1} {$i <= 5} {incr i} {
	set hsort [::webapp::form-text sortdhcpprof$nprof 1 5 5 ""]
	set hprof [::webapp::form-menu nomdhcpprof$nprof 1 0 $lprof {}]
	lappend lines [list Normal $hsort $hprof]
	incr nprof
    }

    set listdhcpprof [::arrgen::output "html" $conf(tabdhcpprofil) $lines]

    #
    # End of script: output page and close database
    #

    d urlset "%URLFORM%" $conf(next) {}
    d result $conf(page-edit) [list \
				[list %TITRE% $title] \
				[list %ORGGRP% $orggrp] \
				[list %NEWGRP% $newgrp] \
				[list %ADMIN% $admin] \
				[list %DROITSMTP% $droitsmtp] \
				[list %DROITTTL% $droitttl] \
				[list %DROITMAC% $droitmac] \
				[list %DROITGENL% $droitgenl] \
				[list %MESSAGEGROUPE% $msggrp] \
				[list %LISTECOR% $listecor] \
				[list %LISTVIEWS% $listviews] \
				[list %LISTDOMAINS% $listdomains] \
				[list %LISTNETS% $listnets] \
				[list %LISTPERMS% $listperms] \
				[list %LISTEDREQ% $listdreq] \
				[list %LISTEDHCPPROFILS% $listdhcpprof] \
			    ]
}

##############################################################################
# Modify group
##############################################################################

d cgi-register {action mod} {
    {confirm		1 1}
    {orggrp		1 1}
    {newgrp		1 1}
    {admin		1 1}
    {droitsmtp		1 1}
    {droitttl		1 1}
    {droitmac		1 1}
    {droitgenl		1 1}
    {login[0-9]+	0 9999}
    {viewname[0-9]+	0 9999}
    {viewsort[0-9]+	0 9999}
    {viewsel[0-9]+	0 9999}
    {sortdom[0-9]+	0 9999}
    {domain[0-9]+	0 9999}
    {rolemail[0-9]+	0 9999}
    {sortnet[0-9]+	0 9999}
    {net[0-9]+		0 9999}
    {dhcp[0-9]+		0 9999}
    {acl[0-9]+		0 9999}
    {addr[0-9]+		0 9999}
    {allow[0-9]+	0 9999}
    {sortdhcpprof[0-9]+	0 9999}
    {nomdhcpprof[0-9]+	0 9999}
    {eqrw[0-9]+		0 9999}
    {eqallow[0-9]+	0 9999}
    {eqpat[0-9]+	0 9999}
} {
    global conf
    global ah

    #
    # Create group of orphans if needed
    #

    set idorphan -1
    pg_select $dbfd "SELECT idgrp FROM global.groupe WHERE nom = ''" tab {
	set idorphan $tab(idgrp)
    }

    if {$idorphan == -1} then {
	set sql "INSERT INTO global.groupe
				(nom, admin, droitsmtp, droitttl, droitmac, droitgenl)
			    VALUES ('', 0, 0, 0, 0, 0)"
	if {! [::pgsql::execsql $dbfd $sql msg]} then {
	    d error [mc "Cannot create group of orphaned users (%s)" $msg]
	} 
	pg_select $dbfd "SELECT idgrp FROM global.groupe WHERE nom = ''" tab {
	    set idorphan $tab(idgrp)
	}
    }

    #
    # In which case are we?
    #

    set state [string equal $orggrp "::nouveau"][string equal $newgrp ""] 
    switch $state {
	11 {
	    d error [mc "You must type a name for the group"]
	}
	01 {
	    set do "del"
	}
	10 {
	    set do "add"
	    val-group $dbfd $newgrp 0
	    set msgact [mc "creation of group %s" $newgrp]
	    set idgrp -1
	}
	00 {
	    set do "mod"
	    set msgact [mc "modification of group %s" $newgrp]
	    set idgrp [val-group $dbfd $orggrp 1]

	    # Renaming
	    if {$newgrp ne $orggrp} then {
		val-group $dbfd $newgrp 0
	    }
	}
    }

    #
    # Group removal
    #

    if {$do eq "del"} then {
	set idgrp [val-group $dbfd $orggrp 1]
	if {$confirm ne "yes"} then {
	    # Ask for confirmation
	    set ftab(confirm)	{yes}
	    set lfields [array names ftab]
	    set hidden  [::webapp::hide-parameters $lfields ftab]
	    d urlset "%URLFORM%" $conf(next) {}
	    d result $conf(page-confdel) [list \
					[list %ORGGRP%	$orggrp] \
					[list %HIDDEN%	$hidden] \
				    ]
	} else {
	    # Proceed to removal
	    del-group $dbfd $idgrp $idorphan
	    d result $conf(page-del) [list \
					[list %ORGGRP%	$orggrp] \
				    ]
	}
	exit 0
    }

    #
    # Everything which follows is related to group creation or
    # modification of an existing group.
    #
    
    #
    # Test various permissions
    #

    foreach f {admin droitsmtp droitttl droitmac droitgenl} {
	set $f [set v [string trim [lindex $ftab($f) 0]]]
	if {$v ne "0" && $v ne "1"} then {
	    d error [mc {Invalid value '%1$s' for form variable '%2$s'} $v $f]
	}
    }

    #
    # Test logins:
    # - read all logins
    # - notice orphans to re-assign to this group
    # - signal an error if the login is already belonging to another group
    # - notice logins to create
    # - notice logins to remove
    #
    
    # read all logins from database

    set sql "SELECT corresp.login, groupe.nom, groupe.idgrp
		FROM global.corresp, global.groupe
		WHERE corresp.idgrp = groupe.idgrp"
    pg_select $dbfd $sql tab {
	if {$tab(nom) eq ""} then {
	    set torph($tab(login)) ""
	} else {
	    set tcor($tab(login)) [list $tab(idgrp) $tab(nom)]
	}
    }

    set lcorcreate {}
    set lcorassign {}
    set lcordelete {}
    set n 1
    while {[info exists ftab(login$n)]} {
	set login [string trim [lindex $ftab(login$n) 0]]
	if {$login ne ""} then {
	    if {[info exists torph($login)]} then {
		lappend lcorassign $login
	    } elseif {[info exists tcor($login)]} then {
		if {$idgrp != [lindex $tcor($login) 0]} then {
		    set g [lindex $tcor($login) 1]
		    d error [mc {Login '%1$s' already assigned to group '%2$s'} $login $g]
		}
		unset tcor($login)
	    } else {
		lappend lcorcreate $login
	    }
	}
	incr n
    }

    foreach login [array names tcor] {
	if {[lindex $tcor($login) 0] == $idgrp} then {
	    lappend lcordelete $login
	}
    }

    #
    # Test view validity and build the list of view-ids
    #

    foreach lv [::pgsql::getcols $dbfd dns.view "" "" {idview name}] {
	set idview [lindex $lv 0]
	set name   [lindex $lv 1]
	set tabv($name) $idview
    }

    set lidview {}
    set n 1
    while {[info exists ftab(viewsort$n)] && [info exists ftab(viewname$n)]} {
	set sort [string trim [lindex $ftab(viewsort$n) 0]]
	if {[string length $sort] > 0} then {
	    if {! [regexp -- {^[0-9]+$} $sort]} then {
		d error [mc "Invalid view sort class '%s'" $sort]
	    }

	    set viewname [string trim [lindex $ftab(viewname$n) 0]]
	    if {! [info exists tabv($viewname)]} then {
		d error [mc "Invalid view '%s'" $viewname]
	    }

	    if {! [info exists ftab(viewsel$n)]} then {
		set ftab(viewsel$n) 0
	    }
	    set viewsel [string trim [lindex $ftab(viewsel$n) 0]]
	    if {! [regexp -- {^[01]$} $viewsel]} then {
		d error [mc "Invalid selection mode '%s'" $viewsel]
	    }

	    lappend lidview [list $sort $tabv($viewname) $viewsel]
	}

	incr n
    }

    #
    # Test domain validity and build the list of domain-ids
    #

    foreach ld [::pgsql::getcols $dbfd dns.domain "" "" {iddom name}] {
	lassign $ld iddom name
	set tabdom($name) $iddom
    }

    set liddom {}
    set n 1
    while {[info exists ftab(sortdom$n)] && [info exists ftab(domain$n)]} {
	set sort [string trim [lindex $ftab(sortdom$n) 0]]
	if {[string length $sort] > 0} then {
	    if {! [regexp -- {^[0-9]+$} $sort]} then {
		d error [mc "Invalid domain sort class '%s'" $sort]
	    }

	    set domain [string trim [lindex $ftab(domain$n) 0]]
	    if {! [info exists tabdom($domain)]} then {
		d error [mc "Invalid domain '%s'" $domain]
	    }

	    if {! [info exists ftab(rolemail$n)]} then {
		set ftab(rolemail$n) 0
	    }
	    set rolemail [string trim [lindex $ftab(rolemail$n) 0]]
	    if {! [regexp -- {^[01]$} $rolemail]} then {
		d error [mc "Invalid mail role '%s'" $rolemail]
	    }

	    lappend liddom [list $sort $tabdom($domain) $rolemail]
	}

	incr n
    }

    #
    # Test network ids and build a list
    #

    foreach ld [::pgsql::getcols $dbfd dns.network "" "" {idnet addr4 addr6 dhcp}] {
	set idnet [lindex $ld 0]
	set laddr {}
	foreach i {1 2} {
	    set a [lindex $ld $i]
	    if {$a ne ""} then {
		lappend laddr $a
	    }
	}
	set tabnet($idnet) $laddr
	set tabdhcp($idnet) [lindex $ld 3]
    }

    set lidnet {}
    set n 1
    while {[info exists ftab(sortnet$n)] && [info exists ftab(net$n)]} {
	set sort [string trim [lindex $ftab(sortnet$n) 0]]
	if {[string length $sort] > 0} then {
	    if {! [regexp -- {^[0-9]+$} $sort]} then {
		d error [mc "Invalid network sort class '%s'" $sort]
	    }

	    set idnet [string trim [lindex $ftab(net$n) 0]]
	    if {! [info exists tabnet($idnet)]} then {
		d error [mc "Invalid network id '%s'" $idnet]
	    }

	    if {! [info exists ftab(dhcp$n)]} then {
		set ftab(dhcp$n) 0
	    }
	    set dhcp [string trim [lindex $ftab(dhcp$n) 0]]
	    if {! [regexp -- {^[01]$} $dhcp]} then {
		d error [mc "Invalid DHCP permission '%s'" $dhcp]
	    }

	    if {! [info exists ftab(acl$n)]} then {
		set ftab(acl$n) 0
	    }
	    set acl [string trim [lindex $ftab(acl$n) 0]]
	    if {! [regexp -- {^[01]$} $acl]} then {
		d error [mc "Invalid ACL permission '%s'" $dhcp]
	    }

	    lappend lidnet [list $sort $idnet $dhcp $acl]
	}

	incr n
    }

    #
    # Test syntax of IP permissions
    #

    set n 1
    set lpip {}
    set p_allow {}
    while {[info exists ftab(addr$n)] && [info exists ftab(allow$n)]} {
	set allow_deny [lindex $ftab(allow$n) 0]
	if {! [regexp {^[01]$} $allow_deny]} then {
	    d error [mc "Invalid value '%s' for allow/deny" $allow_deny]
	}

	set addr [string trim [lindex $ftab(addr$n) 0]]
	if {$addr ne ""} then {
	    set m [check-ip-syntax $dbfd $addr "cidr"]
	    if {$m ne ""} then {
		d error [mc "Invalid CIDR '%s'" $addr]
	    }
	    lappend lpip [list $allow_deny $addr]
	    if {$allow_deny} then {
		lappend p_allow $addr
	    }
	}

	incr n
    }

    #
    # Test syntax of equipment permissions
    #

    set n 1
    set ldreq {}
    while {[info exists ftab(eqpat$n)] &&
		[info exists ftab(eqrw$n)] &&
		[info exists ftab(eqallow$n)]} {

	set rw [lindex $ftab(eqrw$n) 0]
	if {!($rw eq "0" || $rw eq "1")} then {
	    d error [mc "Invalid value '%s' for read/write" $rw]
	}

	set allow_deny [lindex $ftab(eqallow$n) 0]
	if {!($allow_deny eq "0" || $allow_deny eq "1")} then {
	    d error [mc "Invalid value '%s' for allow/deny" $allow_deny]
	}

	set pattern [string trim [lindex $ftab(eqpat$n) 0]]
	if {$pattern ne ""} then {
	    if {[catch {regexp $pattern ""} msg]} then {
		d error [mc "Invalid regular expression pattern '%s'" $pattern]
	    }

	    lappend ldreq [list $rw $allow_deny $pattern]
	}

	incr n
    }

    #
    # Test DHCP profile names
    #

    foreach ld [::pgsql::getcols $dbfd dns.dhcpprofil "" "" {iddhcpprofil nom}] {
	set iddhcpprofil [lindex $ld 0]
	set name         [lindex $ld 1]
	set tabdhcpprofil($name) $iddhcpprofil
    }

    set lidprof {}
    set n 1
    while {[info exists ftab(sortdhcpprof$n)] && [info exists ftab(nomdhcpprof$n)]} {
	set sort [string trim [lindex $ftab(sortdhcpprof$n) 0]]
	if {[string length $sort] > 0} then {
	    if {! [regexp -- {^[0-9]+$} $sort]} then {
		d error [mc "Invalid DHCP profile sort class '%s'" $sort]
	    }

	    set dhcpprofil [string trim [lindex $ftab(nomdhcpprof$n) 0]]
	    if {! [info exists tabdhcpprofil($dhcpprofil)]} then {
		d error [mc "Invalid DHCP profile '%s'" $dhcpprofil]
	    }

	    lappend lidprof [list $sort $tabdhcpprofil($dhcpprofil)]
	}

	incr n
    }

    #
    # Test data consistency
    #

    if {$confirm ne "yes"} then {
	#
	# - at least a view
	# - at least a domain
	# - at least a network
	# - each network has one or more IP permissions
	#	which means that a user may access one range in
	#	networks
	# - each IP address permission is within a network
	#	which means that a user do not have larger rights
	#	than allowed networks
	# If one of these conditions is false, we ask for confirmation.
	# This confirmation allow to force rights. A typical example
	# is an administrator which has rights on every network via
	# only one large CIDR.
	#

	set inconsist {}

	# non existant logins

	set u [::webapp::authuser create %AUTO%]
	set n 1
	while {[info exists ftab(login$n)]} {
	    set login [string trim [lindex $ftab(login$n) 0]]
	    if {$login ne ""} then {
		if {[catch {set nb [$ah getuser $login $u]} msg]} then {
		    d error [mc "Authentication base problem: %s" $msg]
		}
		switch $nb {
		    0 {
			lappend inconsist [mc "Login '%s' does not exist" $login]
		    }
		    1 {
			# nothing: it's ok
		    }
		    default {
			d error [mc "Login '%s' found more than once" $login]
		    }
		}
	    }
	    incr n
	}
	
	# at least one view
	if {[llength $lidview] == 0} then {
	    lappend inconsist [mc "No selected view"]
	}

	# at least one domain
	if {[llength $liddom] == 0} then {
	    lappend inconsist [mc "No selected domain"]
	}

	# at least one network
	if {[llength $lidnet] == 0} then {
	    lappend inconsist [mc "No selected network"]
	}

	# authorize DHCP needs that the network be DHCP-enabled
	foreach r $lidnet {
	    set idnet [lindex $r 1]
	    set dhcp [lindex $r 2]
	    if {$dhcp && ! $tabdhcp($idnet)} then {
		lappend inconsist [mc "Network '%s' is not DHCP enabled" $tabnet($idnet)]
	    }
	}

	# every network must at least have a IP address permission
	foreach r $lidnet {
	    set idnet [lindex $r 1]
	    foreach addr $tabnet($idnet) {
		set perm 0
		foreach cidr $p_allow {
		    pg_select $dbfd "SELECT '$addr' >>= '$cidr' AS result" tab {
			set result $tab(result)
		    }
		    if {$result eq "t"} then {
			set perm 1
			break
		    }
		}
		if {! $perm} then {
		    lappend inconsist [mc "No 'allow' permission found for network '%s'" $addr]
		}
	    }
	}

	# no "allow" permission must be larger than a network
	foreach cidr $p_allow {
	    set larger 0
	    foreach r $lidnet {
		set idnet [lindex $r 1]
		foreach addr $tabnet($idnet) {
		    set sql "SELECT cidr '$addr' << cidr '$cidr' AS result"
		    pg_select $dbfd $sql tab {
			set result $tab(result)
		    }
		    if {$result eq "t"} then {
			set larger 1
			break
		    }
		}
	    }

	    if {$larger} then {
		lappend inconsist [mc "'Allow' permission '%s' is too large" $cidr]
	    }
	}

	#
	# If any inconsistency is detected, announce it/them and ask
	# for confirmation.
	#

	if {[llength $inconsist] > 0} then {
	    set ftab(confirm)	{yes}
	    set lfields [array names ftab]
	    set hidden  [::webapp::hide-parameters $lfields ftab]
	    set message [join $inconsist "<BR>\n"]
	    d urlset "%URLFORM%" $conf(next) {}
	    d result $conf(page-conf) [list \
					[list %MSGACT%	$msgact] \
					[list %ORGGRP%	$orggrp] \
					[list %HIDDEN%	$hidden] \
					[list %MESSAGE%	$message] \
			    ]
	    exit 0
	}
    }

    #
    # If we get here, data are consistent, or we have been confirmed.
    # We must then store data in the database.
    # All modifications are done by removing all elements, and then
    # re-inserting them from input.
    #

    set ltab {global.groupe global.corresp
		dns.p_network dns.p_ip dns.p_dom
		dns.dr_dhcpprofil} 
    d dblock $ltab

    # Create group if needed

    if {$do eq "add"} then {
	set qnewgrp [::pgsql::quote $newgrp]
	set sql "INSERT INTO global.groupe
				(nom, admin, droitsmtp, droitttl, droitmac, droitgenl)
			VALUES ('$qnewgrp', $admin, $droitsmtp, $droitttl, $droitmac, $droitgenl)"
	if {! [::pgsql::execsql $dbfd $sql msg]} then {
	    d dbabort [mc "add %s" $newgrp] $msg
	} 
    } else {

	# Existing group editing

	set qorggrp [::pgsql::quote $orggrp]

	if {$orggrp ne $newgrp} then {
	    # Group renaming
	    set qnewgrp [::pgsql::quote $newgrp]
	    set sql "UPDATE global.groupe SET nom = '$qnewgrp' WHERE nom = '$qorggrp'"
	    if {! [::pgsql::execsql $dbfd $sql msg]} then {
		d dbabort [mc "rename %s" $orggrp]
	    }
	}
    }

    # Get group id
    set qnewgrp [::pgsql::quote $newgrp]
    set idgrp -1
    pg_select $dbfd "SELECT idgrp FROM global.groupe WHERE nom = '$qnewgrp'" tab {
	set idgrp $tab(idgrp)
    }
    if {$idgrp == -1} then {
	d error [mc "Internal error: group '%s' not found" $newgrp]
    }

    # Update group attributes
    lappend cmd "UPDATE global.groupe
		    SET admin = $admin,
			droitsmtp = $droitsmtp,
			droitttl = $droitttl,
			droitmac = $droitmac,
			droitgenl = $droitgenl
		    WHERE idgrp = $idgrp"

    # Create or assign users
    if {[llength $lcorcreate] > 0} then {
	foreach login $lcorcreate {
	    set qlogin [::pgsql::quote $login]
	    lappend cmd "INSERT INTO global.corresp (login,present,idgrp)
			    VALUES ('$qlogin',1,$idgrp)"
	}
    }
    if {[llength $lcorassign] > 0} then {
	foreach login $lcorassign {
	    set qlogin [::pgsql::quote $login]
	    lappend cmd "UPDATE global.corresp SET idgrp = $idgrp
			    WHERE login = '$qlogin'"
	}
    }

    # Re-assign all deleted users to the group of orphans
    if {[llength $lcordelete] > 0} then {
	foreach login $lcordelete {
	    set qlogin [::pgsql::quote $login]
	    lappend cmd "UPDATE global.corresp SET idgrp = $idorphan
			    WHERE login = '$qlogin'"
	}
    }

    # Delete all unneeded users
    lappend cmd "DELETE FROM global.corresp 
			WHERE idgrp = $idorphan 
			    AND idcor NOT IN (SELECT DISTINCT idcor FROM dns.rr)"

    # Authorized views for this group
    lappend cmd "DELETE FROM dns.p_view WHERE idgrp = $idgrp"
    foreach e $lidview {
	lassign $e sort idview selected
	lappend cmd "INSERT INTO dns.p_view (idgrp, idview, sort, selected)
			VALUES ($idgrp, $idview, $sort, $selected)"
    }

    # Authorized domains for this group
    lappend cmd "DELETE FROM dns.p_dom WHERE idgrp = $idgrp"
    foreach e $liddom {
	lassign $e sort iddom rolemail
	lappend cmd "INSERT INTO dns.p_dom (idgrp, iddom, sort, rolemail)
			VALUES ($idgrp, $iddom, $sort, $rolemail)"
    }

    # Authorized networks for this group
    lappend cmd "DELETE FROM dns.p_network WHERE idgrp = $idgrp"
    foreach r $lidnet {
	lassign $r sort idnet dhcp acl
	lappend cmd "INSERT INTO dns.p_network (idgrp, idnet, sort, dhcp, acl)
			VALUES ($idgrp, $idnet, $sort, $dhcp, $acl)"
    }

    # IP permissions associated with the group
    lappend cmd "DELETE FROM dns.p_ip WHERE idgrp = $idgrp"
    foreach e $lpip {
	lassign $e allow_deny addr
	lappend cmd "INSERT INTO dns.p_ip (idgrp, addr, allow_deny)
					VALUES ($idgrp, '$addr', $allow_deny)"
    }

    # Equipment permissions (topo) associated with the group
    lappend cmd "DELETE FROM topo.dr_eq WHERE idgrp = $idgrp"
    foreach e $ldreq {
	lassign $e rw allow_deny pattern
	set pattern [::pgsql::quote $pattern]
	lappend cmd "INSERT INTO topo.dr_eq (idgrp, rw, allow_deny, pattern)
				VALUES ($idgrp, $rw, $allow_deny, '$pattern')"
    }

    # DHCP profiles associated with the group
    lappend cmd "DELETE FROM dns.dr_dhcpprofil WHERE idgrp = $idgrp"
    foreach e $lidprof {
	lassign $e sort iddhcpprofil
	lappend cmd "INSERT INTO dns.dr_dhcpprofil (idgrp, iddhcpprofil, tri)
			VALUES ($idgrp, $iddhcpprofil, $sort)"
    }

    #
    # Proceed to database modification
    #

    foreach sql $cmd {
	if {! [::pgsql::execsql $dbfd $sql msg]} then {
	   d dbabort [mc "modify %s" $orggrp] $msg
	}
    }

    d dbcommit [mc "modify %s" $orggrp]

    #
    # Get group characteristics
    #

    set h [display-group $dbfd $idgrp]
    lassign $h \
		tabdroits tablogins tabnets tabcidralone \
		tabviews tabdomains tabdhcpprofils tabdreq

    #
    # End of script: output page and close database
    #

    d result $conf(page-mod) [list \
			    [list %NEWGRP% $newgrp] \
			    [list %TABLOGINS% $tablogins] \
			    [list %TABDROITS% $tabdroits] \
			    [list %TABVIEWS% $tabviews] \
			    [list %TABDOMAINS% $tabdomains] \
			    [list %TABNETS% $tabnets] \
			    [list %TABCIDRALONE% $tabcidralone] \
			    [list %TABDHCPPROFILS% $tabdhcpprofils] \
			    [list %TABDREQ% $tabdreq] \
			]
}

##############################################################################
# Main procedure
##############################################################################

d cgi-dispatch "admin" "admin"
