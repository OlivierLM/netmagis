#!%TCLSH%

#
# List user's permissions
#
# Called by: index
#
# Parameters (form or url): none
#
# History
#   2002/05/21 : pda/jean : design
#   2002/07/09 : pda      : add nologin
#   2003/05/13 : pda/jean : use auth base
#   2005/04/06 : pda      : add dhcp profiles
#   2010/12/10 : pda      : i18n
#   2010/12/25 : pda      : use cgi-dispatch
#

#
# Template pages used by this script
#

set conf(page)		profile.html
set conf(lastlogin)	10

set conf(tablastlogin) {
    global {
	chars {10 normal}
	align {left}
	botbar {yes}
	columns {25 25 25 25}
    }
    pattern Title {
	vbar {yes}
	column { chars {bold} }
	vbar {yes}
	column { chars {bold} }
	vbar {yes}
	column { chars {bold} }
	vbar {yes}
	column { chars {bold} }
	vbar {yes}
    }
    pattern Normal {
	vbar {yes}
	column { }
	vbar {yes}
	column { }
	vbar {yes}
	column { }
	vbar {yes}
	column { }
	vbar {yes}
    }
}

#
# Netmagis general library
#

source %LIBNETMAGIS%

# ::webapp::cgidebug ; exit

##############################################################################
# Main procedure
##############################################################################

d cgi-register {} {
    {login	0 1}
} {
    global conf

    #
    # Display user informations.
    #

    set user	[display-user tabuid]

    #
    # Display last logins
    #

    set sql "SELECT start, ip, stop, stopreason FROM global.wtmp
		    WHERE idcor = $tabuid(idcor)
		    ORDER BY start DESC
		    LIMIT $conf(lastlogin)"
    set lines {}
    lappend lines [list "Title" [mc "Login time"] \
				[mc "IP address"] \
				[mc "Logout time"] \
				[mc "Logout reason"] \
			    ]
    pg_select $dbfd $sql tab {
	lappend lines [list "Normal" \
			    $tab(start) $tab(ip) $tab(stop) $tab(stopreason)]
    }
    set lastlogin [::arrgen::output "html" $conf(tablastlogin) $lines]

    #
    # Display group informations.
    #

    lassign [display-group $dbfd $tabuid(idgrp)] \
	tabperm \
	tabuser \
	tabnetworks \
	tabcidralone \
	tabviews \
	tabdomains \
	tabdhcpprofile \
	tabpermeq \
	tabl2only

    if {$tabcidralone eq ""} then {
	set titlecidralone ""
    } else {
	set titlecidralone [mc "Permissions not associated with any network"]
    }

    #
    # End of script: output page and close database
    #

    d result $conf(page) [list \
			    [list %CORRESP% $user] \
			    [list %LASTLOGIN% $lastlogin] \
			    [list %TABNETWORKS% $tabnetworks] \
			    [list %TITLECIDRALONE% $titlecidralone] \
			    [list %TABCIDRALONE% $tabcidralone] \
			    [list %TABDOMAINS% $tabdomains] \
			    [list %TABVIEWS% $tabviews] \
			    [list %TABDHCPPROFILE% $tabdhcpprofile] \
			    [list %TABPERMEQ% $tabpermeq] \
			    [list %TABL2ONLY% $tabl2only] \
			    ]
}

##############################################################################
# Main procedure
##############################################################################

d cgi-dispatch "dns" ""

