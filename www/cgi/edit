#!%TCLSH%

#
# Choose which type of modification to apply to an IP address.
#
# Called by: net (host list by view or address map)
#
# Parameters (form or url):
#   - adr: IP address (v4 at this time)
#   - idview (optional): view id (or all possible views if not provided)
#
# History
#   2010/10/10 : pda      : specification
#   2010/12/11 : pda      : i18n
#   2010/12/25 : pda      : use cgi-dispatch
#   2012/11/29 : pda/jean : add views
#

#
# Template pages used by this script
#

set conf(page)		edit.html

#
# Next actions
# 

set conf(nextdel)	"del"
set conf(nextmod)	"mod"
set conf(nextlist)	"net"

#
# Netmagis general library
#

source %LIBNETMAGIS%

# ::webapp::cgidebug ; exit

##############################################################################
# Main procedure
##############################################################################

d cgi-register {} {
    {adr	1 1}
    {idview	0 1}
} {
    global conf

    #
    # Check parameters
    #

    set addr $adr

    set msg [check-ip-syntax $dbfd $addr "inet"]
    if {$msg ne ""} then {
	d error $msg
    }

    #
    # Check that IP address belongs to one of our networks
    #

    set idcor $tabuid(idcor)

    if {! [check-authorized-ip $dbfd $idcor $addr]} then {
	d error [mc "You don't have rights on '%s'" $addr]
    }

    #
    # Get all possible views (if idview is not given) or get
    # only filtered views (if idview is given)
    #

    if {$idview == ""} then {
	set idviews {}
    } else {
	set idviews [list $idview]
    }
    set msg [filter-views-for-host $dbfd tabuid "addr" $adr $idviews chkv]
    if {$msg ne ""} then {
	d error $msg
    }

    set idviews $chkv(idviews)
    set nok $chkv(nok)

    #
    # Display selected actions for all views
    #

    set actions ""
    foreach idv $idviews {
	lassign $chkv($idv) vn msg t

	array unset trr
	array set trr $t

	set fqdn "$trr(nom).$trr(domaine)"

	if {$msg eq ""} then {
	    set vact ""

	    #
	    # Delete name
	    #
	    d urlset "" $conf(nextdel) [list \
						[list "name" $trr(nom)] \
						[list "domain" $trr(domaine)] \
						[list "idviews" $idv] \
					    ]
	    d urladdnext ""
	    set a [mc {<a href="%1$s">Remove</a> host '%2$s'} [d urlget ""] $fqdn]
	    append vact [::webapp::helem "li" $a]
	    append vact "\n"

	    #
	    # Delete IP address (if more than one IP address for this host)
	    #

	    set lip [rr-ip-by-view trr $idv]
	    if {[llength $lip] > 1} then {
		d urlset "" $conf(nextdel) [list \
						    [list "addr" $addr] \
						    [list "idviews" $idv] \
						]
		d urladdnext ""
		set a [mc {<a href="%1$s">Remove address '%2$s'</a> from '%3$s' (all other addresses of this host will stay)} [d urlget ""] $addr $fqdn]
		append vact [::webapp::helem "li" $a]
		append vact "\n"
	    }

	    #
	    # Modify informations
	    #

	    d urlset "" $conf(nextmod) [list \
						[list "action" "edit"] \
						[list "nom" $trr(nom)] \
						[list "domaine" $trr(domaine)] \
						[list "idviews" $idv] \
					    ]
	    d urladdnext ""
	    set a [mc {<a href="%1$s">Edit informations</a> of '%2$s'} [d urlget ""] $fqdn]
	    append vact [::webapp::helem "li" $a]
	    append vact "\n"

	    set vact [::webapp::helem "ul" $vact]

	    #
	    # Display view name only if more than view is available
	    #

	    lassign [html-host-info $dbfd trr $idv] link desc
	    set title [mc {Address %1$s matches %2$s in view '%3$s'} $addr $link $vn]

	    set html "$title\n$desc\n$vact"
	    if {$nok <= 1} then {
		append actions $html
	    } else {
		append actions [::webapp::helem "li" $html]
	    }
	}
    }

    if {$nok > 1} then {
	set actions [::webapp::helem "ul" $actions]
    }

    #
    # Next action
    #

    switch -- [d nextprog] {
	map { set format "domap" }
	list -
	default { set format "dolist" }
    }
    d urlset "%URLBACK%" $conf(nextlist) [list \
						[list $format "yes"] \
						[d nextargs] \
					    ]

    #
    # End of script: output page and close database
    #

    d result $conf(page) [list \
				[list %ADR%         $addr] \
				[list %NOM%         $trr(nom)] \
				[list %DOMAINE%     $trr(domaine)] \
				[list %ACTIONS%     $actions] \
			    ]
}

##############################################################################
# Main procedure
##############################################################################

d cgi-dispatch "dns" ""

