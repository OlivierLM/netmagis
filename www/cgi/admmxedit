#!%TCLSH%

#
# Display data for MX modification
#
# Called by: admmxsel
#
# Parameters (form or url): none
#
# History
#   2003/04/24 : pda/jean : design
#   2003/05/13 : pda/jean : use auth base
#   2010/12/06 : pda      : i18n
#

#
# Template pages used by this script
#

set conf(err)		erreur.html
set conf(page)		admmxedit.html

#
# Next actions
# 

set conf(next)		"admmxmodif"

#
# Script parameters
#

set conf(form) {
	{nom		1 1}
	{domaine	1 1}
}

#
# Netmagis general library
#

source %LIBDNS%

# ::webapp::cgidebug ; exit

##############################################################################
# Main procedure
##############################################################################

proc main {} {
    global conf

    #
    # Initialization
    #

    ::dnscontext create d
    d init-cgi "admin" $conf(err) "admin" $conf(form) ftab dbfd tabuid

    set name   [lindex $ftab(nom) 0]
    set domain [lindex $ftab(domaine) 0]

    #
    # Check MX 
    #

    set msg [check-authorized-mx $dbfd $tabuid(idcor) $name iddom $domain exists trr]
    if {$msg ne ""} then {
	d error $msg
    }

    #
    # Present MX informations
    #

    if {$exists} then {
	set idrr $trr(idrr)
    } else {
	set idrr -1
    }

    set cwidth {10 50 40}
    set ctitle [list \
		    [list "text" [mc "Priority"]] \
		    [list "text" [mc "Name"]] \
		    [list "text" [mc "Domain"]] \
		    ]
    set menudom [couple-domains $dbfd $tabuid(idcor) ""]
    set cspec [list \
		{priorite {string 5}  {}} \
		{nom      {string 30} {}} \
		[list domaine [list menu $menudom] {}] \
	    ]
    set sql "SELECT m.mx, m.priorite, r.nom, d.nom AS domaine
		FROM dns.rr_mx m, dns.rr r, dns.domaine d
		WHERE m.idrr = $idrr
			AND m.mx = r.idrr
			AND r.iddom = d.iddom
		ORDER BY m.priorite, d.nom, r.nom
		"
    set idnum "mx"

    set msg [display-tabular $cwidth $ctitle $cspec $dbfd $sql $idnum tableau]
    if {$msg ne ""} then {
	d error $msg
    }

    #
    # End of script: output page and close database
    #

    d urlset "%URLFORM%" $conf(next) {}
    d result $conf(page) [list \
				[list %TABLEAU% $tableau] \
				[list %NOM%     $name] \
				[list %DOMAINE% $domain] \
			    ]
}

::webapp::cgi-exec main $debug
