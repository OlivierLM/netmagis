#!%TCLSH%

#
# Script pour présenter la page de modification d'une ou plusieurs
# interfaces de l'équipement.
#
# Called by: script eq
#
# Parameters (form or url):
#	- eq: equipment name
#	- iface : nom de l'interface, ou vide pour toutes les interfaces de l'eq
#
# History
#   2010/11/03 : pda        : design
#   2010/11/30 : pda/jean   : add sensor
#


#
#


#
# Template pages used by this script
#

set conf(err)		erreur.html
set conf(page)		ifchg.html

#
# Next actions
# 

set conf(next)		"%HOMEURL%/bin/traiteifchg"
set conf(nextifchg)	"%HOMEURL%/bin/ifchg"

#
# Script parameters
#


set conf(form)	{
    {eq			1 1}
    {iface		0 1}
}

#
#

source %LIBDNS%

#
#

# ::webapp::cgidebug ; exit



##############################################################################
# Main procedure
##############################################################################

proc main {} {
    global conf

    #
    # Initialization
    #

    ::dnscontext create d
    d init-cgi "topo" $conf(err) "" $conf(form) ftab dbfd login tabuid

    ::webapp::import-vars ftab
    foreach f $conf(form) {
	set var [lindex $f 0]
	set $var [string trim [lindex [set $var] 0]]
    }

    #
    # Lire les informations de l'équipement dans le graphe
    # Ces informations sont filtrées par tabuid qui n'affiche
    # que les vlans autorisés.
    #

    set l [eq-iflist $eq tabuid]

    lassign $l eq type model location liferr iflist arrayif arrayvlan
    array set tabiface $arrayif
    array set tabvlan  $arrayvlan

    #
    # En cas d'erreur, on sort prématurément
    # Ce cas correspond à une (ou plusieurs) interface qui serait
    # modifiable mais non consultable.
    #

    if {[llength $liferr] > 0} then {
	set texte "Incohérence sur les droits d'accès pour les interfaces : "
	append texte [join $liferr " "]
	return [list $titre $texte]
    }

    #
    # Préparer les listes de vlans pour les menus
    #
    # Note : un vlan est soit un vlan VoIP, soit un vlan "standard".
    #

    set lvlan [list [list -1 "Désactiver le port"]]
    set lvoip [list [list -1 "Désactiver la VoIP sur ce port"]]
    foreach id [lsort -integer [array names tabvlan]] {
	lassign $tabvlan($id) desc voip
	if {$desc eq "-"} then {
	    set desc ""
	} else {
	    set desc [binary format H* $desc]
	}
	if {$voip} then {
	    lappend lvoip [list $id "$id ($desc)"]
	} else {
	    lappend lvlan [list $id "$id ($desc)"]
	}
    }

    set nvoip [llength $lvoip]

    #
    # Est-ce qu'il faut présenter toutes les interfaces ou juste une ?
    #

    if {$iface ne ""} then {
	#
	# Il y a un nom d'interface. Présenter juste cette interface
	#

	if {! [info exists tabiface($iface)]} then {
	    d error "L'interface $iface n'est pas trouvée sur $eq"
	}

	#
	# Get status information relative to this equipment and this
	# interface.
	#

	set eqsta [eq-graph-status $dbfd $eq $iface]

	#
	# Récupérer les caractéristiques de l'interface
	#

	lassign $tabiface($iface) nom edit radio ifstat ifmode desc lien natif
	set trunk [lreplace $tabiface($iface) 0 7]

	if {$edit ne "edit"} then {
	    d error "Vous ne pouvez modifier l'interface $iface sur $eq"
	}

	#
	# Préparer les informations
	#

	set mode "mono"
	set titre "Modification de l'interface $iface sur $eq"
	set menuif [::webapp::form-hidden iface $iface]

	# proposer l'option "plusieurs interfaces" s'il y a effectivement
	# plus d'une interface modifiable
	set nifmod 0
	foreach i $iflist {
	    if {[lindex $tabiface($i) 1] eq "edit"} then {
		incr nifmod
	    }
	}
	if {$nifmod > 1} then {
	    d urlset "" $conf(nextifchg) [list [list "eq" $eq]]
	    set url [d urlget ""]
	    set multi [::webapp::helem "p" \
				[format {%1$s %2$s %3$s} \
				    "Vous pouvez également" \
				    [::webapp::helem "a" \
						{modifier plusieurs interfaces} \
						"href" $url \
					] \
				    "simultanément"
				] \
			    ]
	} else {
	    set multi ""
	}

	# proposer le menu de description d'interface
	if {$desc eq "-"} then {
	    set desc ""
	} else {
	    set desc [::webapp::html-string [binary format H* $desc]]
	}
	set libifordesc "Description"
	set menuifordesc [::webapp::form-text desc 1 40 40 $desc]
	append menuifordesc " (caractères spéciaux autorisés : -+/()&.:#_)"

	append libifordesc [::webapp::form-hidden "iface" $iface]

	# proposer le menu de vlan
	if {$ifmode eq "Disabled"} then {
	    set pos -1
	} else {
	    set pos [lsearch -index 0 $lvlan $natif]
	}
	if {$pos == -1} then {
	    set lsel {}
	} else {
	    set lsel [list $pos]
	}
	set menuvlan [::webapp::form-menu "vlan" 1 0 $lvlan $lsel]

	# proposer le menu de vlan voip si nécessaire
	if {$nvoip > 1} then {
	    set pos -1
	    if {$ifmode eq "Trunk"} then {
		# rechercher, parmi les vlans taggés, celui qui
		# est voip (le cas échéant)
		foreach v $trunk {
		    lassign $v id desc stat
		    # rechercher le vlan dans la liste du menu
		    set pos [lsearch -index 0 $lvoip $id]
		    if {$pos != -1} then {
			break
		    }
		}
	    }
	    if {$pos == -1} then {
		set lsel {}
	    } else {
		set lsel [list $pos]
	    }
	    set libvoip "VoIP"
	    set menuvoip [::webapp::form-menu "voip" 1 0 $lvoip $lsel]
	} else {
	    set libvoip ""
	    set menuvoip [::webapp::form-hidden "voip" -1]
	}

	# proposer le menu de modification de sonde
	if {$tabuid(admin)} then {
	    if {$ifstat eq "-"} then {
		set ifstat ""
	    }
	    set libstat "Sondes"
	    set menustat [::webapp::form-text "stat" 1 40 40 $ifstat]
	} else {
	    set libstat ""
	    set menustat ""
	}

    } else {
	#
	# Présenter toutes les interfaces
	#

	set titre "Modification des interfaces de $eq"
	set mode "multi"

	#
	# Get status information relative to this equipment and all
	# its interfaces.
	#
	set eqsta [eq-graph-status $dbfd $eq]

	# liste des interfaces
	set lif {}
	foreach i $iflist {
	    set edit [lindex $tabiface($i) 1]
	    if {$edit eq "edit"} then {
		lappend lif [list $i $i]
	    }
	}
	set nif [llength $lif]
	set libifordesc "Interfaces"
	set menuifordesc [::webapp::form-menu "iface" $nif 1 $lif {}]
	set multi ""

	# proposer le menu de vlan
	set menuvlan [::webapp::form-menu "vlan" 1 0 $lvlan {}]

	# proposer le menu de vlan voip si nécessaire
	if {$nvoip > 1} then {
	    set libvoip "VoIP"
	    set menuvoip [::webapp::form-menu "voip" 1 0 $lvoip {}]
	} else {
	    set libvoip ""
	    set menuvoip [::webapp::form-hidden "voip" -1]
	}

	# le menu de modification de sonde est vide
	set libstat ""
	set menustat ""
    }

    #
    # Script suivant
    #

    d urlset "%URLFORM%" $conf(next) {}

    #
    # End of script: output page and close database
    #

    d result $conf(page) [list \
			    [list %EQSTA%        $eqsta] \
			    [list %EQ%           $eq] \
			    [list %TITRE%        $titre] \
			    [list %MODE%         $mode] \
			    [list %MULTI%        $multi] \
			    [list %LIBIFORDESC%  $libifordesc] \
			    [list %MENUIFORDESC% $menuifordesc] \
			    [list %MENUVLAN%     $menuvlan] \
			    [list %LIBVOIP%      $libvoip] \
			    [list %MENUVOIP%     $menuvoip] \
			    [list %LIBSTAT%      $libstat] \
			    [list %MENUSTAT%     $menustat] \
			]
}

::webapp::cgi-exec main %DEBUG%
