#!%TCLSH%


#
# Script de présentation de la page des paramètres de la base
#
# Appelé par : index.htgt
#
# Paramètres (formulaire ou URL) : aucun
#
# Historique
#   2003/12/08 : pda      : création à partir de sos
#   2004/10/13 : pda      : ajout des paramètres DHCP
#   2010/10/25 : pda      : librairisation des paramètres
#

set conf(homeurl)	%HOMEURL%

#
# Chemins utilisés par les scripts
#

set conf(lib)		%DESTDIR%/lib
set conf(libdns)	$conf(lib)/libdns.tcl

#
# Définition des noms des pages "à trous"
#

set conf(err)		$conf(lib)/erreur.html
set conf(page)		$conf(lib)/admparliste.html

#
# Scripts suivants (actions de formulaire)
# 

set conf(next)		"%HOMEURL%/bin/admparmodif"
set conf(nexthelp)	"%HOMEURL%/bin/admparhelp"


#
# Les outils du parfait concepteur de pages Web dynamiques...
#

lappend auto_path %PKGTCL%
package require webapp
package require pgsql
package require arrgen

#
# Le tableau servant à présenter les paramètres
#

set conf(tableau) {
    global {
	chars {12 normal}
	columns {50 50}
    }
    pattern {Normal1} {
	vbar {no}
	column {
	    multicolumn {2}
	    color {909090}
	    align {center}
	    botbar {no}
	}
	vbar {no}
    }
    pattern {Normal2} {
	vbar {no}
	column {
	    align {right}
	    botbar {no}
	    format {raw}
	}
	vbar {no}
	column {
	    align {left}
	    botbar {no}
	    format {raw}
	}
	vbar {no}
    }
    pattern {Boutons} {
	vbar {no}
	column {
	    multicolumn {2}
	    color {10C090}
	    align {center}
	    botbar {no}
	    format {raw}
	}
	vbar {no}
    }
}

#
# On y va !
#

# ::webapp::cgidebug ; exit

source $conf(libdns)

##############################################################################
# Programme principal
##############################################################################

proc main {} {
    global conf

    #
    # Initialisation
    #

    ::dnscontext create d
    d init-cgi "admin" $conf(err) "admin" {} ftab dbfd login tabcor

    #
    # Mise en forme des données dans le formulaire
    #

    d urlset "" $conf(nexthelp) {}
    set url [d urlget ""]
    append url {#%1$s}
    set urlaide [::webapp::helem "a" {%2$s} "href" $url]

    set donnees {}

    foreach class [dnsconfig class] {
	lappend donnees [list Normal1 [dnsconfig desc $class]]

	foreach key [dnsconfig keys $class] {

	    set type   [dnsconfig keytype $key]
	    set desc   [dnsconfig desc $key]
	    set curval [dnsconfig get $key]

	    switch -- [lindex $type 0] {
		bool {
		    set html [::webapp::form-yesno $key $curval {%1$sOui %2$sNon}]
		}
		string {
		    set html [::webapp::form-text $key 1 60 0 $curval]
		}
		text {
		    set html [::webapp::form-text $key 20 70 0 $curval]
		}
		menu {
		    set lmenu [lindex $type 1]
		    set html [::webapp::form-menu $key 1 0 $lmenu {}]
		}
		default {
		    set html [::webapp::helem "font" "ERREUR : type de '$key'" \
						"color" "#ff0000"]
		}
	    }

	    set aide [format $urlaide $key $desc]
	    lappend donnees [list Normal2 $aide $html]
	}
    }
    set html "<INPUT TYPE=submit VALUE=\"Enregistrer\">"
    append html " <INPUT TYPE=reset VALUE=\"Réinitialiser\">"
    lappend donnees [list Boutons $html]

    set tab [::arrgen::output "html" $conf(tableau) $donnees]

    #
    # Génération de la page contenant le formulaire
    #

    d urlset "%URLFORM%" $conf(next) {}
    d result $conf(page) [list \
				[list %TAB% $tab] \
			    ]
}

::webapp::cgi-exec main %DEBUG%
