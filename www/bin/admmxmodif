#!%TCLSH%


#
# Script pour enregistrer une modification de mx
#
# Appelé par : admmxsel.htgt
#
# Paramètres (formulaire ou URL) : aucun
#
# Historique
#   2003/04/24 : pda/jean : création
#   2003/05/13 : pda/jean : utilisation de la base d'authentification
#   2004/03/04 : pda/jean : mise en commun de check-mx
#   2007/10/25 : jean     : log des actions de modification
#

set conf(homeurl)	%HOMEURL%

#
# Chemins utilisés par les scripts
#

set conf(lib)		%DESTDIR%/lib
set conf(libdns)	$conf(lib)/libdns.tcl

#
# Définition des noms des pages "à trous"
#

set conf(err)		$conf(lib)/erreur.html
set conf(page)		$conf(lib)/admmxmodif.html

#
# Scripts suivants (actions de formulaire)
# 

# (aucun : page cul de sac)

#
# Quelques paramètres du script
#

set conf(form) {
	{nom			1 1}
	{domaine		1 1}

	{priorite[0-9]+		0 9999}
	{nom[0-9]+		0 9999}
	{domaine[0-9]+		0 9999}

	{prioriten[0-9]+	0 9999}
	{nomn[0-9]+		0 9999}
	{domainen[0-9]+		0 9999}
}

set conf(tableau) {
    global {
	chars {12 normal}
	columns {20 80}
	botbar {yes}
	align {left}
    }
    motif {Titre} {
	title {yes}
	topbar {yes}
	chars {bold}
	vbar {yes}
	colonne { }
	vbar {yes}
	colonne { }
	vbar {yes}
    }
    motif {Normal} {
	vbar {yes}
	colonne { }
	vbar {yes}
	colonne { }
	vbar {yes}
    }
}

#
# Les outils du parfait concepteur de pages Web dynamiques...
#

lappend auto_path %PKGTCL%
package require webapp
package require arrgen
package require pgsql

#
# On y va !
#

# ::webapp::cgidebug ; exit

source $conf(libdns)

##############################################################################
# Fonctions utilitaires
##############################################################################

#
# Insère une liste de mx dans la base
#
# Entrée :
#   - paramètres :
#	- dbfd : accès à la base
#	- idrr : id du RR
#	- lmx : liste au format spécifié dans check-mx
# Sortie :
#   - valeur de retour : chaîne vide si ok, ou message d'erreur
#
# Historique
#   2003/04/25 : pda/jean : conception
#

proc inserer-mx {dbfd idrr lmx} {
    foreach mx $lmx {
	set prio [lindex $mx 0]
	set idmx [lindex $mx 1]
	set sql "INSERT INTO dns.rr_mx (idrr, priorite, mx)
				VALUES ($idrr, $prio, $idmx)"
	if {! [::pgsql::execsql $dbfd $sql msg]} then {
	    return "Insertion du MX impossible ($msg)"
	}
    }
    return ""
}

##############################################################################
# Programme principal
##############################################################################

proc main {} {
    global conf

    #
    # Initialisation
    #

    ::dnscontext create d
    d init-cgi "admin" $conf(err) "admin" $conf(form) ftab dbfd login tabcor

    set nom     [lindex $ftab(nom) 0]
    set domaine [lindex $ftab(domaine) 0]

    #
    # Valider le nom du MX et le domaine
    #

    set msg [check-name-syntax $nom]
    if {[string length $msg] > 0} then {
	d error $msg
    }

    set iddom -1
    set msg [check-domain $dbfd $tabcor(idcor) iddom domaine ""]
    if {$msg ne ""} then {
	d error $msg
    }

    #
    # Récupérer les informations sur ce nom s'il existe dans la base
    #

    set existe [read-rr-by-name $dbfd $nom $iddom tabrr]

    #
    # S'il existe, vérifier que ce n'est pas déjà un A ou un CNAME
    # ou autre chose encore qui ne soit pas des MX
    #

    if {$existe} then {
	if {[llength $tabrr(ip)] > 0} then {
	    d error "'$nom' a déjà des adresses IP."
	}
	if {[llength $tabrr(cname)] > 0} then {
	    d error "'$nom' est un alias."
	}

	#
	# Le MX existe, il faut maintenant vérifier que l'utilisateur
	# a bien accès à tous les domaines des MX référencés.
	#
	foreach mx $tabrr(mx) {
	    set idmx [lindex $mx 1]
	    if {[read-rr-by-id $dbfd $idmx tabmx] == 0} then {
		d error "ERREUR INTERNE : table rr_mx référence le RR $idmx, non trouvé dans la table rr"
	    }
	    set iddom $tabmx(iddom)
	    set msg [check-domain $dbfd $tabcor(idcor) iddom tabmx(domaine) ""]
	    if {$msg ne ""} then {
		d error [format [mc {MX '%1$s' points to a domain on which you don't have rights\n%2$s}] \
				"$tabmx(nom).$tabmx(domaine)" $msg]
	    }
	    set tabprio($idmx) [lindex $mx 0]
	}
    }

    #
    # Parcourir la liste des champs de formulaire et constituer une
    # liste de la forme :
    #		{{prio idmx} ... }
    # où :
    #  - prio = priorité numérique (syntaxe entière ok)
    #  - idmx = id d'un RR existant
    #

    set lmx {}
    foreach c [array names ftab] {
	if {[regexp {^priorite(n?)([0-9]+)$} $c bidon n idmx]} then {
	    set idxprio priorite$n$idmx
	    set idxnom  nom$n$idmx
	    set idxdom  domaine$n$idmx
	    if {[info exists ftab($idxprio)] && \
			    [info exists ftab($idxnom)] && \
			    [info exists ftab($idxdom)] \
		    } then {
		set fprio [string trim [lindex $ftab($idxprio) 0]]
		set fnom  [string trim [lindex $ftab($idxnom)  0]]
		set fdom  [string trim [lindex $ftab($idxdom)  0]]
		if {! [string equal $fprio ""]} then {
		    set msg ""
		    set mx [check-mx $dbfd \
					    $fprio $fnom $fdom \
					    $tabcor(idcor) msg]
		    if {! [string equal $msg ""]} then {
			d error "$msg pour $fnom.$fdom"
		    }
		    if {[info exists tmx([lindex $mx 1])]} then {
			d error "$fnom.$fdom spécifié deux fois."
		    }
		    lappend lmx $mx
		}
	    } else {
		d error "Formulaire non conforme ($idxprio, $idxnom, $idxdom)"
	    }
	}
    }

    #
    # Débuter la transaction
    #

    if {! [::pgsql::lock $dbfd {} msg]} then {
	d error "Verrouillage de la base impossible: $msg"
    }

    #
    # Traitement des insertions et suppressions
    #

    if {$existe} then {
	#
	# Supprimer tous les MX éventuels au préalable.
	#

	set sql "DELETE FROM dns.rr_mx WHERE idrr = $tabrr(idrr)"
	if {! [::pgsql::execsql $dbfd $sql msg]} then {
	    ::pgsql::unlock $dbfd "abort" m
	    d error "Suppression des anciens MX impossible: $msg"
	}

	#
	# Si la liste lmx est vide, supprimer le RR
	#

	if {[llength $lmx] == 0} then {
	    #
	    # Supprimer le RR
	    #
	    set sql "DELETE FROM dns.rr WHERE idrr = $tabrr(idrr)"
	    if {! [::pgsql::execsql $dbfd $sql msg]} then {
		::pgsql::unlock $dbfd "abort" m
		d error "Suppression du RR '$nom.$domaine' impossible: $msg"
	    }
	} else {
	    #
	    # Insérer les RR des MX
	    #
	    set msg [inserer-mx $dbfd $tabrr(idrr) $lmx]
	    if {! [string equal $msg ""]} then {
		::pgsql::unlock $dbfd "abort" m
		d error "Ajout des MX impossible: $msg"
	    }
	}
    } else {
	#
	# Le RR n'existe pas.
	#

	if {[llength $lmx] == 0} then {
	    #
	    # Cas sans objet : l'utilisateur a demandé à créer un
	    # MX, mais n'a saisi aucun MX.
	    #
	    ::pgsql::unlock $dbfd "abort" m
	    d error "Pas de création de MX pour '$nom.$domaine'."
	} else {
	    #
	    # Créer le RR
	    #

	    # XXX : pas de commentaire ni de responsable... peut-être plus tard
	    set msg [add-rr $dbfd $nom $iddom "" 0 "" 0 -1 "" "" "" \
				$tabcor(idcor) tabrr]
	    if {! [string equal $msg ""]} then {
		d error "Impossible d'insérer '$nom.$domaine' : $msg($msg)"
	    }

	    #
	    # Insérer les RR des MX
	    #
	    set msg [inserer-mx $dbfd $tabrr(idrr) $lmx]
	    if {! [string equal $msg ""]} then {
		::pgsql::unlock $dbfd "abort" m
		d error "Ajout des MX impossible: $msg"
	    }
	}
    }

    #
    # Déverrouillage, et enregistrement des modifications avant la sortie
    #

    if {! [::pgsql::unlock $dbfd "commit" msg]} then {
        ::pgsql::unlock $dbfd "abort" m
        return "Déverrouillage impossible, modification annulée ('$msg')"
    }

    #
    # Récapituler les informations ajoutées dans la base.
    #

    if {[llength $lmx] > 0} then {
	set donnees {}
	lappend donnees {Titre Priorité Nom}
	set lm {}
	foreach mx $lmx {
	    # priorité idmx
	    read-rr-by-id $dbfd [lindex $mx 1] tabmx
	    lappend donnees [list Normal \
				[lindex $mx 0] \
				$tabmx(nom).$tabmx(domaine) \
			    ]
	    lappend lm "$tabmx(nom).$tabmx(domaine)"
	}
	set tableau [::arrgen::output "html" $conf(tableau) $donnees]
	d writelog "ajoutmx" "ajout des mx [join $lm {, }] pour $nom.$domaine"
    } else {
	set tableau "RR supprimé."
	d writelog "supprmx" "suppression du RR pour $nom.$domaine"
    }

    #
    # Fin du script : sortie de la page et fermeture de la base
    #

    d result $conf(page) [list \
				[list %TABLEAU% $tableau] \
				[list %NOM%     $nom] \
				[list %DOMAINE% $domaine] \
			    ]
}

::webapp::cgi-exec main %DEBUG%
