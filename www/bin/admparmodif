#!%TCLSH%


#
# Script d'enregistrement des modifications des paramètres de la base
#
# Appelé par : adminliste
#
# Paramètres (formulaire ou URL) : les paramètres
#
# Historique
#   2003/12/08 : pda      : création à partir de sos
#   2004/10/13 : pda      : ajout des paramètres DHCP
#   2007/10/25 : jean     : log des actions de modification
#

set conf(homeurl)	%HOMEURL%

#
# Chemins utilisés par les scripts
#

set conf(lib)		%DESTDIR%/lib
set conf(libdns)	$conf(lib)/libdns.tcl

#
# Définition des noms des pages "à trous"
#

set conf(err)		$conf(lib)/erreur.html
set conf(page)		$conf(lib)/admvalide.html

#
# Scripts suivants (actions de formulaire)
# 

set conf(nextaccueil)	"%HOMEURL%/bin/accueil"
set conf(nextadmin)	"%HOMEURL%/bin/admin"
set conf(nextmodif)	"%HOMEURL%/bin/admparliste"

#
# Les outils du parfait concepteur de pages Web dynamiques...
#

lappend auto_path %PKGTCL%
package require webapp
package require pgsql

#
# On y va !
#

# ::webapp::cgidebug ; exit

source $conf(libdns)

##############################################################################
# Programme principal
##############################################################################

proc main {} {
    global conf

    #
    # Initialisation
    #

    ::dnscontext create d
    d init-cgi "admin" $conf(err) "admin" {} ftab dbfd login tabcor

    #
    # Création d'une liste de contrôle de formulaire à partir
    # des clefs de configuration
    #

    set form {}
    foreach key [dnsconfig keys] {
	lappend form [list $key 1 1]
    }
    if {[llength [::webapp::get-data ftab $form]] == 0} then {
	d error "Formulaire non conforme aux spécifications"
    }

    #
    # Enregistrement des paramètres du formulaire
    #

    if {! [::pgsql::lock $dbfd {global.config} msg]} then {
	d error "Verrouillage impossible ('$msg')"
    }

    set lm {}
    foreach key [dnsconfig keys] {
	set val [lindex $ftab($key) 0]
	set msg [dnsconfig set $key $val]
	if {[string equal $msg ""]} then {
	    lappend lm "$key ($val)"
	} else {
	    ::pgsql::unlock $dbfd "abort" m
	    d error "Ereur dans l'enregistrement de '$key' ('$msg')"
	}
    }

    if {! [::pgsql::unlock $dbfd "commit" msg]} then {
	d error "Déverrouillage impossible ('$msg')"
    }

    d writelog "admparmodif" "modification des parametres de l'application : [join $lm ,]"

    #
    # Génération de la page d'accusé de modification
    #

    set typeenclair "paramètres de l'application"

    d urlset "%URL1%" $conf(nextaccueil) {}
    d urlset "%URL2%" $conf(nextadmin) {}
    d urlset "%URL3%" $conf(nextmodif) {}

    d result $conf(page) [list \
				[list %TYPEENCLAIR% $typeenclair] \
			    ]
}

::webapp::cgi-exec main %DEBUG%
