#!%TCLSH%

#
# Script qui affiche des statistiques
# sur les informations contenues dans la base MAC
#
# Historique
#   2005/05/27 : jean : conception
#   2010/11/30 : pda/jean : integration a WebDNS
#

set conf(homeurl)       %HOMEURL%

#
# Chemins utilisés par les scripts
#

set conf(lib)           %DESTDIR%/lib
set conf(libdns)        $conf(lib)/libdns.tcl

#
# Définition des noms des pages "à trous"
#

set conf(err)           $conf(lib)/erreur.html
set conf(page)          $conf(lib)/macstat.html

#
# Quelques paramètres du script
#

set conf(dbmac)          %BASEMAC%


lappend auto_path %PKGTCL%
package require webapp
package require arrgen
package require pgsql

#
# Formats du tableau
#

set conf(tabresultat) {
    global {
        chars {12 normal}
        align {left}
        botbar {yes}
        columns {75 25}
    }   
    pattern Normal {
        vbar {yes}
        column { }
        vbar {yes}
        column { }
        vbar {yes}
    }
}

#
# On y va !
#

# ::webapp::cgidebug ; exit

source $conf(libdns)

#
# Calcule les statistiques sur les adresses
#

proc stat-adr {dbfd} {
    global conf

    # liste de couples description - requetes
    set req {
    "Nombre d'adresses mac uniques"
    "SELECT count(DISTINCT mac) AS r FROM portmac" 
    "Nombre d'adresses IPv4 uniques"
    "SELECT count(DISTINCT ip) AS r FROM ipmac WHERE family(ip)=4"
    "Nombre d'adresses IPv6 uniques"
    "SELECT count(DISTINCT ip) AS r FROM ipmac WHERE family(ip)=6"
    "Nombre d'adresses MAC distinctes associées à une adresse IPv6"
    "SELECT count(DISTINCT(mac)) AS r FROM ipmac WHERE family(ip)=6"
    }

    set data {}

    foreach {description requete} $req {
	set resultat ""
	pg_select $dbfd $requete tab {
	    set resultat $tab(r)
	}
	lappend data [list Normal $description $resultat]
    }
    append tableau [::arrgen::output "html" $conf(tabresultat) $data]

    return $tableau
}

#
# Programme Principal
#

proc main {} {
    global conf

    #
    # Initialisation
    #

    ::dnscontext create d
    d init-cgi "mac" $conf(err) "" {} ftab dbfd login tabcor

    #
    # Accès à la base MAC
    #

    if {[catch {set dbfdmac [pg_connect -conninfo $conf(dbmac)]} msg]} then {
        ::webapp::error-exit $conf(err) $msg
    }

    #
    # Génère le tableau html de résultat
    #

    set tableau [stat-adr $dbfdmac]

    #
    # Sortie du résultat
    #

    d result $conf(page) [list \
                                [list %TITRE%   "Statistiques sur les adresses MAC/IP"] \
                                [list %TABLEAU% $tableau] \
                            ]
}

::webapp::cgi-exec main
