#!%TCLSH%


#
# Script de présentation des paramètres DHCP d'un réseau
#
# Called by: script dhcp ou dchpsel
#
# Parameters (form or url):
#   - reseau : id du réseau sélectionné
#
# History
#   2004/10/05 : pda/jean : design
#


#
#


#
# Next actions
# 

set conf(next)		"%HOMEURL%/bin/dhcpmodif"

#
# Script parameters
#

set conf(form)	{
	{idreseau	1 1}
}

#
# Template pages used by this script
#

set conf(err)		erreur.html
set conf(page)		dhcpedit.html

#
#

source %LIBDNS%

#
#

# ::webapp::cgidebug ; exit


##############################################################################
# Main procedure
##############################################################################

proc main {} {
    global conf

    #
    # Initialization
    #

    ::dnscontext create d
    d init-cgi "dns" $conf(err) "" $conf(form) ftab dbfd login tabuid

    set idgrp $tabuid(idgrp)

    #
    # Valider le réseau
    #

    set idreseau [lindex $ftab(idreseau) 0]
    set lcidr [check-netid $dbfd $idreseau $idgrp "dhcp" 4 msg]
    if {[llength $lcidr] == 0} then {
	d error $msg
    }
    if {[llength $lcidr] != 1} then {
	d error "Erreur interne: trop de CIDR trouvés !"
    }

    #
    # Préparer les éléments du tableau n+5
    #

    set largeurs {25 25 20 10 10 10}
    set titre {{text Min} {text Max}
		{text Domaine}
		{text {Durée par défaut du bail}}
		{text {Durée maximum du bail}}
		{text Profil DHCP}
	    }

    set menudom {}
    set sql "SELECT d.iddom, d.nom FROM dns.domaine d, dns.dr_dom dr
			WHERE d.iddom = dr.iddom
				AND dr.idgrp = $idgrp
			ORDER BY dr.tri ASC, d.nom ASC"
    pg_select $dbfd $sql tab {
	lappend menudom [list $tab(iddom) $tab(nom)]
    }

    set menudhcp {}
    set sql "	(SELECT 0 AS iddhcpprofil, 'Aucun profil' AS nom)
		UNION
		(SELECT p.iddhcpprofil AS iddhcpprofil, p.nom AS nom
		    FROM dns.dr_dhcpprofil dr, dns.dhcpprofil p, global.corresp c
		    WHERE c.idcor = $tabuid(idcor)
			AND dr.idgrp = c.idgrp
			AND dr.iddhcpprofil = p.iddhcpprofil
		    ORDER BY dr.tri ASC)
		ORDER BY nom"
    pg_select $dbfd $sql tab {
	lappend menudhcp [list $tab(iddhcpprofil) $tab(nom)]
    }

    set spec {
	    {min		{string 15}	{}}
	    {max		{string 15}	{}}
	    {iddom		{menu {%MENUDOM%}}	{}}
	    {default_lease_time	{string 10}	0}
	    {max_lease_time	{string 10}	0}
	    {iddhcpprofil	{menu {%MENUDHCP%}}	0}
	}
    regsub -- "%MENUDOM%" $spec "$menudom" spec
    regsub -- "%MENUDHCP%" $spec "$menudhcp" spec
    set sql "SELECT * FROM dns.dhcprange
			WHERE min <<= '$lcidr'
			    AND max <<= '$lcidr'
			    AND valide_dhcprange_grp ($idgrp, min, max)
			ORDER BY min"
    set id iddhcprange

    #
    #

    set errmsg [display-tabular $largeurs $titre $spec $dbfd $sql $id tableau]
    if {[string length $errmsg] > 0} then {
	d error $errmsg
    }

    #
    # End of script: output page and close database
    #

    d urlset "%URLFORM%" $conf(next) {}
    d result $conf(page) [list \
				[list %IDRESEAU% $idreseau] \
				[list %RESEAU% $lcidr] \
				[list %TABLEAU% $tableau] \
			    ]
}

::webapp::cgi-exec main %DEBUG%
