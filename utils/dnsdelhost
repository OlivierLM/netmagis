#!%TCLSH%


#
# Script pour supprimer une machine (ou un alias) dans la base
#
# Syntaxe :
#   dnsdelhost <fqdn>
#
# Historique
#   2004/10/01 : pda/jean : conception
#   2007/10/25 : jean     : log des actions de modification
#   2010/12/18 : pda      : use new install system
#

source %LIBNETMAGIS%

##############################################################################
# Petites fonctions utilitaires
##############################################################################

proc syntax-error {argv0} {
    regsub {.*/} $argv0 {} argv0
    puts stderr "usage: $argv0 fqdn"
    exit 1
}

##############################################################################
# Suppression d'une machine
##############################################################################

#
# Suppression d'une machine dans la base
#
# Entrée :
#   - dbfd : accès à la base
#   - idcor : id du correspondant faisant la suppression
#   - fqdn : nom du host
# Sortie :
#   - valeur de retour : message d'erreur, ou chaîne vide si ok.
#
# Historique
#   2004/10/01 : pda/jean : conception
#

proc del-host {dbfd login idcor fqdn} {
    #
    # Validation du nom du host
    #

    set msg [check-fqdn-syntax $dbfd $fqdn nom domain iddom]
    if {! [string equal $msg ""]} then {
	return $msg
    }
    set nom [string tolower $nom]

    #
    # Valider le nom fourni
    #

    set msg [check-authorized-host $dbfd $idcor $nom $domain trr "del-name"]
    if {[string length $msg] > 0} then {
	return $msg
    }

    #
    # Vérifier que le nom demandé existe
    #

    if {[string equal $trr(idrr) ""]} then {
	return "Le nom '$nom.$domain' n'existe pas."
    }

    #
    # Noter si c'est un alias
    #

    if {[string equal $trr(cname) ""]} then {
	set alias 0

	#
	# Si ce n'est pas un alias, il doit y avoir au moins une adresse IP
	#
	if {[string equal $trr(ip) ""]} then {
	    return "Le nom '$nom.$domain' n'est pas une machine."
	}

	set logevt "supprnom"
	set logmsg "suppression totale de $fqdn"

    } else {
	set alias 1

	#
	# C'est un alias. Lisons les informations pour afficher
	# une page éventuelle de confirmation
	#
	if {! [read-rr-by-id $dbfd $trr(cname) trrref]} then {
	    return "Erreur interne. Alias non trouvé"
	}

	set p "$trrref(nom).$trrref(domain)"
	set logevt "suppralias"
	set logmsg "suppression de l'alias $fqdn -> $p"
    }

    #
    # Effectuer la suppression dans la base.
    #

    if {! [::pgsql::lock $dbfd {} msg]} then {
	return "Transaction impossible : $msg"
    }

    if {$alias} then {
	if {! [del-alias-by-id $dbfd $trr(idrr) msg]} then {
	    ::pgsql::unlock $dbfd "abort" m
	     return "La suppression a échoué ($msg)"
	}
    } else {
	#
	# Ce n'est pas un alias. Supprimer toutes les dépendances
	# du RR
	# - les aliases pointant vers cet objet
	# - les MX
	# - les adresses IP
	#
	if {! [del-rr-and-dependancies $dbfd trr msg]} then {
	    ::pgsql::unlock $dbfd "abort" m
	    return "La suppression a échoué ($msg)"
	}
    }

    #
    # Enregistrer les modifications dans la base
    #
    if {! [::pgsql::unlock $dbfd "commit" msg]} then {
       ::pgsql::unlock $dbfd "abort" m
       return "La suppression a échoué ($msg)"
    }

    #
    # Écrire le log
    #

    d writelog $logevt "$logmsg (ligne de commande)"

    return ""
}


##############################################################################
# Programme principal
##############################################################################

proc main {argv0 argv} {
    global conf

    #
    # Initialisation des accès
    #

    set msg [d init-script dbfd $argv0 false tabcor]
    if {$msg ne ""} then {
	puts stderr "$msg"
	puts stderr "Aborted."
	return 1
    }

    #
    # Validation des arguments
    #

    if {[llength $argv] != 1} then {
	syntax-error $argv0
	return 1
    }

    set fqdn [string tolower [lindex $argv 0]]

    set msg [del-host $dbfd $tabcor(login) $tabcor(idcor) $fqdn]
    if {! [string equal $msg ""]} then {
	puts stderr $msg
	puts stderr "Aborted."
	return 1
    }

    #
    # Déconnexion de la base
    #

    d end
    return 0
}

exit [main $argv0 $argv]
