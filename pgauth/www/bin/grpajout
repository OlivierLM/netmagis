#!%TCLSH%

#
# Script pour afficher les groupes et leurs utilisateurs
#
# Appelé par : index.htgt
#
# Paramètres (formulaire ou URL) : aucun
#
# Historique
#   2003/05/30 : pda/jean : création
#   2003/06/27 : pda      : utilisation de cgi-exec
#

set conf(homeurl)	%HOMEURL%

#
# Chemins utilisés par les scripts
#

set conf(pkg)		%PKGTCL%
set conf(lib)		%DESTDIR%/lib
set conf(libauth)	$conf(lib)/libauth.tcl

#
# Définition des noms des pages "à trous"
#

set conf(err)		$conf(lib)/erreur.html
set conf(page)		$conf(lib)/grpajout.html

#
# Quelques paramètres du script
#

set conf(auth)		%AUTH%
set conf(nologin)	%NOLOGIN%

#
# Les outils du parfait concepteur de pages Web dynamiques...
#

lappend auto_path $conf(pkg)
package require auth
package require webapp
package require pgsql
package require arrgen

#
# Format du tableau utilisé
#

set conf(tabgrp) {
    global {
	chars {12 normal}
	align {left}
	botbar {yes}
	columns {25 75}
    }
    pattern Groupe {
	vbar {yes}
	column {
	    chars {bold}
	}
	vbar {yes}
	column {}
	vbar {yes}
    }
    pattern Util {
	vbar {yes}
	column {
	    multicolumn {2}
	}
	vbar {yes}
    }
}


#
# On y va !
#

# ::webapp::cgidebug ; exit

source $conf(libauth)

##############################################################################
# Programme principal
##############################################################################

proc main {} {
    global conf

    #
    # Initialisation
    #

    init-auth $conf(nologin) $conf(auth) $conf(err) {} ftab login

    #
    # Récupérer la liste des groupes
    #

    if {! [::auth::lsgroup tabgrp]} then {
	::webapp::error-exit $conf(err) \
			"Impossible de récupérer la liste des groupes."
    }

    #
    # Parcourir et mettre en forme la liste des groupes
    #

    set donnees {}
    foreach g [lsort [array names tabgrp]] {
	set descr   [lindex $tabgrp($g) 0]
	lappend donnees [list Groupe $g $descr]
    }

    #
    # Formatter le tableau
    #

    set groupes [::arrgen::output "html" $conf(tabgrp) $donnees]

    #
    # Sortie du résultat
    #

    ::webapp::send html [::webapp::file-subst $conf(page) \
				[list \
					[list %GROUPES% $groupes] \
				    ] \
			    ]

    #
    # Déconnexion de la base
    #

    ::auth::close
}

::webapp::cgi-exec main %DEBUG%
